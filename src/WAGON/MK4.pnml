//Mark 3 Carriage

/*

Liveries done:
BR Blue
Intercity
Virgin
Midland Mainline (Blue)
EMT
First Fag
First Barbie
FGW
GWR

Liveries to do:
Scotrail
ATW
One
Virgin Pretendolino
Greater Anglia
Network Rail
Chiltern Railways

*/

//Spritesets

spriteset(gfx_mk4_intercity				, "gfx/ELECTRIC_LOCO/class_91.png"){ template_Mk2_single		(260,100)	}
spriteset(gfx_mk4_gner					, "gfx/ELECTRIC_LOCO/class_91.png"){ template_Mk2_single		(260,125)	}
spriteset(gfx_mk4_nxec	 				, "gfx/ELECTRIC_LOCO/class_91.png"){ template_Mk2_single		(260,150)	}
spriteset(gfx_mk4_east_coast			, "gfx/ELECTRIC_LOCO/class_91.png"){ template_Mk2_single		(260,175)	}
spriteset(gfx_mk4_vtec_front			, "gfx/ELECTRIC_LOCO/class_91.png"){ template_Mk2_double		(260,200)	}
spriteset(gfx_mk4_vtec_back				, "gfx/ELECTRIC_LOCO/class_91.png"){ template_Mk2_double_reverse(260,200)	}
spriteset(gfx_mk4_vtec_buffet			, "gfx/ELECTRIC_LOCO/class_91.png"){ template_Mk2_double		(260,225)	}
spriteset(gfx_mk4_vtec_buffet_reverse	, "gfx/ELECTRIC_LOCO/class_91.png"){ template_Mk2_double_reverse(260,225)	}
spriteset(gfx_mk4_gc					, "gfx/ELECTRIC_LOCO/class_91.png"){ template_Mk2_single		(260,275)	}

spriteset(gfx_mk4_gangway				, "gfx/ELECTRIC_LOCO/class_91.png"){ template_Mk2_double		(260,250)	}

spriteset(gfx_mk4_purchase_intercity	, "gfx/ELECTRIC_LOCO/class_91.png"){ template_purchase			(438, 100)	}
spriteset(gfx_mk4_purchase_gner			, "gfx/ELECTRIC_LOCO/class_91.png"){ template_purchase			(438, 125)	}
spriteset(gfx_mk4_purchase_nxec			, "gfx/ELECTRIC_LOCO/class_91.png"){ template_purchase			(438, 150)	}
spriteset(gfx_mk4_purchase_east_coast	, "gfx/ELECTRIC_LOCO/class_91.png"){ template_purchase			(438, 175)	}
spriteset(gfx_mk4_purchase_vtec			, "gfx/ELECTRIC_LOCO/class_91.png"){ template_purchase			(438, 200)	}
spriteset(gfx_mk4_purchase_gc			, "gfx/ELECTRIC_LOCO/class_91.png"){ template_purchase			(438, 275)	}

spriteset(gfx_mk4_windows_normal		, "gfx/ELECTRIC_LOCO/class_91.png"){ template_Mk2_single		(  0, 25)	}
spriteset(gfx_mk4_windows_buffet		, "gfx/ELECTRIC_LOCO/class_91.png"){ template_Mk2_double		(  0, 50)	}
spriteset(gfx_mk4_windows_buffet_rev 	, "gfx/ELECTRIC_LOCO/class_91.png"){ template_Mk2_double_reverse(  0, 50)	}

//Graphics switches

//VTEC switches

switch(FEAT_TRAINS, SELF, sw_mk4_gfx_vtec_pos_check_front, position_in_vehid_chain){
	0..2: gfx_mk4_vtec_front	;
	3	: gfx_mk4_vtec_buffet_reverse	;
	gfx_mk4_vtec_back ;
}

switch(FEAT_TRAINS, SELF, sw_mk4_gfx_vtec_pos_check_back, position_in_vehid_chain_from_end){
	0..2: gfx_mk4_vtec_back	;
	3	: gfx_mk4_vtec_buffet	;
	gfx_mk4_vtec_front ;
}

switch(FEAT_TRAINS, PARENT, sw_mk4_gfx_vtec_rev_check, vehicle_is_reversed){
	1 : sw_mk4_gfx_vtec_pos_check_back ;
	sw_mk4_gfx_vtec_pos_check_front ;
}


//Automatic liveries

switch(FEAT_TRAINS, SELF, sw_mk4_gfx_auto, current_year){
	ERA_SECTORISATION : gfx_mk4_intercity ; // IC
	2007..2009	: gfx_mk4_nxec ; // NXEC
	2010..2014	: gfx_mk4_east_coast ; // EAST COAST
	2015..2019	: sw_mk4_gfx_vtec_rev_check ;
	2020..4096	: gfx_mk4_gc ;
	gfx_mk4_gner ; // GNER
}


//Manual liveries

switch(FEAT_TRAINS, SELF, sw_mk4_gfx_manual, cargo_subtype){
	0 : gfx_mk4_intercity ; // IC
	2 : gfx_mk4_nxec ; // NXEC
	3 : gfx_mk4_east_coast ; // EAST COAST
	4 : sw_mk4_gfx_vtec_rev_check ; // VTEC/LNER
	5 : gfx_mk4_gc ;
	gfx_mk4_gner ; //GNER
}

switch(FEAT_TRAINS, SELF, sw_mk4_gfx_manual_check, livery_choice){
	1 : sw_mk4_gfx_manual ;
	sw_mk4_gfx_auto ;
}


//Windows switches

switch(FEAT_TRAINS, SELF, sw_mk4_gfx_window, position_in_vehid_chain ){
	3 : gfx_mk4_windows_buffet ;
	gfx_mk4_windows_normal ;
}

switch(FEAT_TRAINS, SELF, sw_mk4_gfx_window_reverse, position_in_vehid_chain_from_end ){
	3 : gfx_mk4_windows_buffet_rev ;
	gfx_mk4_windows_normal ;
}

switch(FEAT_TRAINS, PARENT, sw_mk4_gfx_window_reverse_check, vehicle_is_reversed){
	1 : sw_mk4_gfx_window_reverse ;
	sw_mk4_gfx_window ;
}


//Gangway switches

/*switch(FEAT_TRAINS, SELF, sw_mk4_gfx_gangway_dvt_check, count_veh_id(9035) > 0){
	1 : gfx_mk4_gangway ;
}

	0 : sw_mk4_gfx_gangway_dvt_check ;*/
switch(FEAT_TRAINS, SELF, sw_mk4_gfx_gangway_pos_check, position_in_vehid_chain_from_end > 0){
	1 : gfx_mk4_gangway ;
}

//Sprite stacking

switch(FEAT_TRAINS, SELF, sw_mk4_sprite_stack, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]){
	0 : sw_mk4_gfx_manual_check ;
	1 : sw_mk4_gfx_window_reverse_check ;
	2 : sw_mk4_gfx_gangway_pos_check ;
}


//Refit text

switch(FEAT_TRAINS, SELF, sw_mk4_refit_text, cargo_subtype){
	0 : string(STR_LIVERY_INTERCITY) ;	// IC
	1 : string(STR_LIVERY_GNER) ;		// GNER
	2 : string(STR_LIVERY_NXEC) ;		// NXEC
	3 : string(STR_LIVERY_ESCS) ;		// EAST COAST
	4 : string(STR_LIVERY_VTEC_LNER) ;	// VTEC/LNER
	5 : string(STR_LIVERY_GC) ;	// VTEC/LNER
	CB_RESULT_NO_TEXT ;
}

switch(FEAT_TRAINS, SELF, sw_mk4_refit_text_check, livery_choice){
	1 : sw_mk4_refit_text ;
	CB_RESULT_NO_TEXT ;
}


//Purchase sprite switches

switch(FEAT_TRAINS, SELF, sw_mk4_purchase, current_year){
	ERA_SECTORISATION : gfx_mk4_purchase_intercity ;
	2007..2009 : gfx_mk4_purchase_nxec ;
	2010..2014 : gfx_mk4_purchase_east_coast ;
	2015..2019 : gfx_mk4_purchase_vtec ;
	2020..4096: gfx_mk4_purchase_gc ;
	gfx_mk4_purchase_gner ;
}

switch(FEAT_TRAINS, SELF, sw_mk4_purchase_colours, current_year){
	ERA_SECTORISATION : palette_2cc(COLOUR_GREY, COLOUR_RED) ; // IC
	2007..2009	: palette_2cc(COLOUR_GREY, COLOUR_WHITE) ; // NXEC
	2010..2014	: palette_2cc(COLOUR_WHITE, COLOUR_PINK) ; // EAST COAST
	2015..2019	: palette_2cc(COLOUR_RED, COLOUR_WHITE) ; // VTEC
	2020..4096	: palette_2cc(COLOUR_GREY, COLOUR_ORANGE) ;
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_RED) ; // GNER
}

switch(FEAT_TRAINS, SELF, sw_mk4_purchase_colours_2cc_check, colour_scheme_1){
	1 : sw_mk4_purchase_colours ;
	palette_2cc(company_colour1, company_colour1) ;
}



//Colour mapping


switch(FEAT_TRAINS, SELF, sw_mk4_colours_auto, current_year){
	ERA_SECTORISATION : palette_2cc(COLOUR_GREY, COLOUR_RED) ; // IC
	2007..2009	: palette_2cc(COLOUR_GREY, COLOUR_WHITE) ; // NXEC
	2010..2014	: palette_2cc(COLOUR_WHITE, COLOUR_PINK) ; // EAST COAST
	2015..2019	: palette_2cc(COLOUR_RED, COLOUR_WHITE) ; // VTEC
	2020..4096	: palette_2cc(COLOUR_GREY, COLOUR_ORANGE) ;
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_RED) ; // GNER
}

switch(FEAT_TRAINS, SELF, sw_mk4_colours_manual, cargo_subtype){
	0	: palette_2cc(COLOUR_GREY, COLOUR_RED) ; // IC
	2	: palette_2cc(COLOUR_GREY, COLOUR_WHITE) ; // NXEC
	3	: palette_2cc(COLOUR_WHITE, COLOUR_PINK) ; // EAST COAST
	4	: palette_2cc(COLOUR_RED, COLOUR_WHITE) ; // VTEC/LNER
	5	: palette_2cc(COLOUR_GREY, COLOUR_ORANGE) ;
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_RED) ; // GNER
}

switch(FEAT_TRAINS, SELF, sw_mk4_colours_manual_check, livery_choice){
	1 : sw_mk4_colours_manual ;
	sw_mk4_colours_auto ;
}

switch(FEAT_TRAINS, SELF, sw_mk4_colours_2cc_check, colour_scheme_1){
	1 : sw_mk4_colours_manual_check ;
	palette_2cc(company_colour1, company_colour2) ;
}



//Item

item (FEAT_TRAINS, item_mk4, 9040) {
	property {
		name: 							string(STR_NAME_Mk4);
		climates_available: 			ALL_CLIMATES;
		introduction_date:				date(1990, 1, 1);
		model_life:						VEHICLE_NEVER_EXPIRES ;
		retire_early:					1;
		vehicle_life:					40;
		reliability_decay:				11;
		refittable_cargo_classes:		CC_PASSENGERS;
		cargo_allow_refit:				[PASS, TOUR];
		loading_speed:					20;
		cost_factor:					45;
		running_cost_factor:			45;
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		speed:							140 mph;
		misc_flags:						bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		refit_cost:						0;
		track_type:						RAIL;
		ai_special_flag:				AI_FLAG_PASSENGER;
		power:							0 hp;
		running_cost_base:				RUNNING_COST_NONE;
		dual_headed:					0;
		default_cargo_type:				PASS;
		cargo_capacity:					88;
		weight:							40 ton;
		//tractive_effort_coefficient:	0.07;
		//air_drag_coefficient:			0.08;
		length:							8;
		bitmask_vehicle_info:			0;
		cargo_age_period:				195;
	}
	graphics {
		default                 : sw_mk4_sprite_stack ;
		purchase                : sw_mk4_purchase ;
		purchase_colour_mapping : sw_mk4_purchase_colours_2cc_check ;
		cargo_subtype_text      : sw_mk4_refit_text_check ;
		colour_mapping          : sw_mk4_colours_2cc_check ;
	}
}