//Mark 2

/*
	[x+85,y,8,23,-3,-10] //South
	[x+95,y,18,15,-12,-8] //South West
	[x+115,y,33,13,-17,-9] //West
	[x+150,y,18,15,-3,-8] //North West
*/


//#define MK2_BRAKE_CHECK_FRONT ((count_veh_id(9025) == 1) | (count_veh_id(9035) == 1)) & (position_in_vehid_chain_from_end == 0)

//#define MK2_BRAKE_CHECK_BACK (count_veh_id(9025) == 0) & (count_veh_id(9035) == 0) & (position_in_consist == 0)

//Templates

template template_Mk2_single(x,y){
	[x+0,y,8,23,-3,-11] //North
	[x+10,y,20,16,-13,-8] //North East
	[x+32,y,33,13,-17,-9] //East
	[x+67,y,20,16,-5,-8] //South East
}

template template_Mk2_double(x,y){
	[x+0,y,8,23,-3,-11] //North
	[x+10,y,20,16,-13,-8] //North East
	[x+32,y,33,13,-17,-9] //East
	[x+67,y,20,16,-5,-8] //South East
	[x+89,y,8,23,-3,-11] //South
	[x+99,y,20,16,-13,-8] //South West
	[x+121,y,33,13,-17,-9] //West
	[x+156,y,20,16,-5,-8] //North West
}

template template_Mk2_double_reverse(x,y){
	[x+89,y,8,23,-3,-11] //South
	[x+99,y,20,16,-13,-8] //South West
	[x+121,y,33,13,-17,-9] //West
	[x+156,y,20,16,-5,-8] //North West
	[x+0,y,8,23,-3,-11] //North
	[x+10,y,20,16,-13,-8] //North East
	[x+32,y,33,13,-17,-9] //East
	[x+67,y,20,16,-5,-8] //South East
}


//Spritesets

spriteset(gfx_mk2_brblue_f_tso				, "gfx/WAGON/mark_2_2.png"){template_Mk2_single(0,0)}
spriteset(gfx_mk2_brblue_f_bso				, "gfx/WAGON/mark_2_2.png"){template_Mk2_double(0,25)}
spriteset(gfx_mk2_brblue_f_bso_reverse		, "gfx/WAGON/mark_2_2.png"){template_Mk2_double_reverse(0,25)}
spriteset(gfx_mk2_brblue_a_tso				, "gfx/WAGON/mark_2_2.png"){template_Mk2_single(0,50)}
spriteset(gfx_mk2_brblue_a_bso				, "gfx/WAGON/mark_2_2.png"){template_Mk2_double(0,75)}
spriteset(gfx_mk2_brblue_a_bso_reverse		, "gfx/WAGON/mark_2_2.png"){template_Mk2_double_reverse(0,75)}
spriteset(gfx_mk2_intercity_f_tso			, "gfx/WAGON/mark_2_2.png"){template_Mk2_single(0,100)}
spriteset(gfx_mk2_intercity_f_bso			, "gfx/WAGON/mark_2_2.png"){template_Mk2_double(0,125)}
//spriteset(gfx_mk2_intercity_f_dbso		, "gfx/WAGON/mark_2_dbso.png"){template_Mk2_double(0,0)}
spriteset(gfx_mk2_intercity_f_bso_reverse	, "gfx/WAGON/mark_2_2.png"){template_Mk2_double_reverse(0,125)}
spriteset(gfx_mk2_regional_a_tso			, "gfx/WAGON/mark_2_2.png"){template_Mk2_single(0,150)}
spriteset(gfx_mk2_regional_a_bso			, "gfx/WAGON/mark_2_2.png"){template_Mk2_double(0,175)}
spriteset(gfx_mk2_regional_a_bso_reverse	, "gfx/WAGON/mark_2_2.png"){template_Mk2_double_reverse(0,175)}
spriteset(gfx_mk2_nse_a_tso					, "gfx/WAGON/mark_2_2.png"){template_Mk2_single(0,350)}
spriteset(gfx_mk2_nse_a_bso					, "gfx/WAGON/mark_2_2.png"){template_Mk2_double(0,375)}
spriteset(gfx_mk2_nse_a_bso_reverse			, "gfx/WAGON/mark_2_2.png"){template_Mk2_double_reverse(0,375)}
spriteset(gfx_mk2_virgin_f_tso				, "gfx/WAGON/mark_2_2.png"){template_Mk2_single(0,200)}
spriteset(gfx_mk2_virgin_f_bso				, "gfx/WAGON/mark_2_2.png"){template_Mk2_double(0,225)}
spriteset(gfx_mk2_virgin_f_bso_reverse		, "gfx/WAGON/mark_2_2.png"){template_Mk2_double_reverse(0,225)}
spriteset(gfx_mk2_generic_a_tso				, "gfx/WAGON/mark_2_2.png"){template_Mk2_single(0,250)}
spriteset(gfx_mk2_generic_a_bso				, "gfx/WAGON/mark_2_2.png"){template_Mk2_double(0,275)}
spriteset(gfx_mk2_generic_a_bso_reverse		, "gfx/WAGON/mark_2_2.png"){template_Mk2_double_reverse(0,275)}
spriteset(gfx_mk2_directrail_f_tso			, "gfx/WAGON/mark_2_2.png"){template_Mk2_single(0,300)}
spriteset(gfx_mk2_directrail_f_bso			, "gfx/WAGON/mark_2_2.png"){template_Mk2_double(0,325)}
//spriteset(gfx_mk2_directrail_f_dbso		, "gfx/WAGON/mark_2_dbso.png"){template_Mk2_double(0,25)}
spriteset(gfx_mk2_directrail_f_bso_reverse	, "gfx/WAGON/mark_2_2.png"){template_Mk2_double_reverse(0,325)}
spriteset(gfx_mk2_generic_f_tso				, "gfx/WAGON/mark_2_2.png"){template_Mk2_single(0,400)}
spriteset(gfx_mk2_barbie_sleeper_f_tso		, "gfx/WAGON/mark_2_2.png"){template_Mk2_single(0,450)}
spriteset(gfx_mk2_fgw_fag_f_tso				, "gfx/WAGON/mark_2_2.png"){template_Mk2_single(0,475)}
spriteset(gfx_mk2_scr_f_tso					, "gfx/WAGON/mark_2_2.png"){template_Mk2_single(0,500)}
spriteset(gfx_mk2_scr_f_tso_end				, "gfx/WAGON/mark_2_2.png"){template_Mk2_double(0,525)}
spriteset(gfx_mk2_scr_f_tso_end_reverse		, "gfx/WAGON/mark_2_2.png"){template_Mk2_double_reverse(0,525)}

spriteset(gfx_mk2_purchase_brblue          , "gfx/WAGON/mark_2_2.png"){template_purchase(89,50)}
spriteset(gfx_mk2_purchase_regional        , "gfx/WAGON/mark_2_2.png"){template_purchase(89,150)}
spriteset(gfx_mk2_purchase_virgin          , "gfx/WAGON/mark_2_2.png"){template_purchase(89,200)}
spriteset(gfx_mk2_purchase_directrail      , "gfx/WAGON/mark_2_2.png"){template_purchase(89,300)}

spriteset(gfx_mk2_bso_purchase_brblue          , "gfx/WAGON/mark_2_2.png"){template_purchase(178,50)}
spriteset(gfx_mk2_bso_purchase_regional        , "gfx/WAGON/mark_2_2.png"){template_purchase(178,150)}
spriteset(gfx_mk2_bso_purchase_virgin          , "gfx/WAGON/mark_2_2.png"){template_purchase(178,200)}
spriteset(gfx_mk2_bso_purchase_directrail      , "gfx/WAGON/mark_2_2.png"){template_purchase(178,300)}


//Switches




//Graphics Switches

//Mark 2a

switch(FEAT_TRAINS, SELF, sw_mk2a_livery_decider, current_year){
	ERA_BRBLUE         : gfx_mk2_brblue_a_tso ;
	ERA_BRTOPS     : gfx_mk2_brblue_a_tso ;
	ERA_SECTORISATION  : gfx_mk2_regional_a_tso ;
	ERA_PRIVATE_ONE : gfx_mk2_regional_a_tso ;
	gfx_mk2_brblue_a_tso ;
}


//Mark 2f


switch(FEAT_TRAINS, SELF, sw_mk2_scr_check_back, position_in_vehid_chain_from_end){ 0 : gfx_mk2_scr_f_tso_end ; gfx_mk2_scr_f_tso ; }
switch(FEAT_TRAINS, SELF, sw_mk2_scr_check_front, position_in_vehid_chain){ 0 : gfx_mk2_scr_f_tso_end_reverse ; sw_mk2_scr_check_back ; }


switch(FEAT_TRAINS, SELF, sw_mk2f_livery_decider, current_year){
	ERA_BRBLUE        : gfx_mk2_brblue_f_tso ;
    1971..1978     : gfx_mk2_brblue_f_tso ;
	1979..1982     : gfx_mk2_intercity_f_tso ;
	ERA_SECTORISATION : gfx_mk2_intercity_f_tso ;
	ERA_PRIVATE_ONE   : gfx_mk2_virgin_f_tso ;
	ERA_PRIVATE_TWO   : gfx_mk2_directrail_f_tso ;
	gfx_mk2_intercity_f_tso ;
}


switch(FEAT_TRAINS, PARENT, sw_mk2_type_decider_1, vehicle_type_id){
	47 : sw_mk2f_livery_decider ;
	86 : sw_mk2f_livery_decider ;
	87 : sw_mk2f_livery_decider ;
	90 : sw_mk2f_livery_decider ;
	sw_mk2a_livery_decider ;
}


//Manual livery decider

switch(FEAT_TRAINS, SELF, sw_mk2_livery_decider, cargo_subtype){
	1: gfx_mk2_brblue_f_tso ;
	2..3: gfx_mk2_intercity_f_tso ;
	4: gfx_mk2_regional_a_tso ;
	5: gfx_mk2_nse_a_tso ;
	6: gfx_mk2_virgin_f_tso ;
	7: gfx_mk2_directrail_f_tso ;
	8: gfx_mk2_barbie_sleeper_f_tso ;
	9..10: gfx_mk2_generic_f_tso ;
	11: gfx_mk2_fgw_fag_f_tso ;
	12: sw_mk2_scr_check_front ;
	14 : gfx_mk2_brblue_f_tso;
	gfx_mk2_brblue_a_tso ;
}

switch(FEAT_TRAINS, SELF, sw_mk2_livery_choice_decider, livery_choice){
	1 : sw_mk2_livery_decider ; 
	sw_mk2_type_decider_1 ;
}


//Sprite stacking
switch(FEAT_TRAINS, SELF, sw_mk2_default, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]){
	0 : return sw_mk2_livery_choice_decider;
}




//Cargo refit text
switch(FEAT_TRAINS, SELF, sw_mk2_refit_text, cargo_subtype){
	0 : return(string(STR_LIVERY_BRBLUE_SUBTYPE,string(STR_NAME_MK2A))) ;
	1 : return(string(STR_LIVERY_BRBLUE_SUBTYPE,string(STR_NAME_MK2F))) ;
	2 : string(STR_LIVERY_INTERCITY) ;
	3 : string(STR_LIVERY_BR_SCOTRAIL) ;
	4 : string(STR_LIVERY_REG_RAILWAYS) ;
	5 : string(STR_LIVERY_NSE) ;
	6 : string(STR_LIVERY_VIRGIN_TRAINS) ;
	7 : string(STR_LIVERY_DRS) ;
	8 : string(STR_LIVERY_FIRST_SCOT_RAIL, string(STR_LIVERY_SLEEPER)) ;
	9 : string(STR_LIVERY_WESSEX_TRAINS) ;
	10 : string(STR_LIVERY_NETWORK_RAIL) ;
	11 : string(STR_LIVERY_FIRST_FAG, string(STR_LIVERY_EMPTY)) ;
	12 : string(STR_LIVERY_SCR) ;
	13 : string(STR_LIVER_COMP_COLOURS_A);
	14 : string(STR_LIVER_COMP_COLOURS_F);
	return CB_RESULT_NO_TEXT ;
}

switch(FEAT_TRAINS, SELF, sw_mk2_refit_text_decider, livery_choice){
	1 : sw_mk2_refit_text ;
	return CB_RESULT_NO_TEXT ;
}

//Purchase sprite decider
switch(FEAT_TRAINS, SELF, sw_mk2_purchase, current_year){
	ERA_SECTORISATION  : gfx_mk2_purchase_regional ;
	ERA_PRIVATE_ONE : gfx_mk2_purchase_virgin ;
	ERA_PRIVATE_TWO : gfx_mk2_purchase_directrail ;
	gfx_mk2_purchase_brblue ;
}


//Colour mapping

random_switch(FEAT_TRAINS, SELF, sw_mk2_brblue_colour_mapping, TRIGGER_VEHICLE_SERVICE){ 1 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;}
random_switch(FEAT_TRAINS, SELF, sw_mk2_random_sectorised_colourmapping, TRIGGER_VEHICLE_SERVICE){ 1 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE) ;}
random_switch(FEAT_TRAINS, SELF, sw_mk2_random_privatised_colourmapping){ 1 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE) ; 2 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_RED) ; }

switch(FEAT_TRAINS, SELF, sw_mk2_colour_mapping_auto_a, current_year){
	ERA_BRBLUE         : sw_mk2_brblue_colour_mapping ;
	ERA_BRTOPS     : sw_mk2_brblue_colour_mapping ;
	ERA_SECTORISATION  : sw_mk2_random_sectorised_colourmapping ;
	ERA_PRIVATE_ONE : sw_mk2_random_privatised_colourmapping ;
	sw_mk2_brblue_colour_mapping ;
}


random_switch(FEAT_TRAINS, SELF, sw_mk2_intercity_colour_mapping, TRIGGER_VEHICLE_SERVICE){ 1 : palette_2cc(COLOUR_GREY, COLOUR_RED) ; }
random_switch(FEAT_TRAINS, SELF, sw_mk2_virgin_colour_mapping, TRIGGER_VEHICLE_SERVICE){ 1 : palette_2cc(COLOUR_RED, COLOUR_WHITE) ; }
random_switch(FEAT_TRAINS, SELF, sw_mk2_directrail_colour_mapping, TRIGGER_VEHICLE_SERVICE){ 1 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ; }

switch(FEAT_TRAINS, SELF, sw_mk2_colour_mapping_auto_f, current_year){
	ERA_BRBLUE         : sw_mk2_brblue_colour_mapping ;
    1971..1978     : sw_mk2_brblue_colour_mapping ;
	1979..1982     : palette_2cc(COLOUR_GREY, COLOUR_DARK_BLUE) ;
    ERA_SECTORISATION  : sw_mk2_intercity_colour_mapping ;
    ERA_PRIVATE_ONE : sw_mk2_virgin_colour_mapping ;
    ERA_PRIVATE_TWO : sw_mk2_directrail_colour_mapping ;
	sw_mk2_intercity_colour_mapping ;
}


switch(FEAT_TRAINS, PARENT, sw_mk2_colour_mapping_auto_checker_one, vehicle_type_id){
	47 : sw_mk2_colour_mapping_auto_f ;
	86 : sw_mk2_colour_mapping_auto_f ;
	87 : sw_mk2_colour_mapping_auto_f ;
	90 : sw_mk2_colour_mapping_auto_f ;
	sw_mk2_colour_mapping_auto_a ;
}


switch(FEAT_TRAINS, SELF, sw_mk2_colour_mapping_manual, cargo_subtype){
	2 : palette_2cc(COLOUR_GREY, COLOUR_RED) ;
	3 : palette_2cc(COLOUR_GREY, COLOUR_DARK_BLUE) ;
	4 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE) ;
	5 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_RED) ;
	6 : palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
	7 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ;
	8 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	9 : palette_2cc(COLOUR_PINK, COLOUR_WHITE) ;
	10 : palette_2cc(COLOUR_YELLOW, COLOUR_YELLOW) ;
	11 : palette_2cc(COLOUR_DARK_GREEN, COLOUR_YELLOW) ;
	12 : palette_2cc(COLOUR_RED, COLOUR_ORANGE) ;
	13..14: palette_2cc(company_colour1, company_colour2) ;
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, SELF, sw_mk2_colour_mapping_manual_check, livery_choice){
	1 : sw_mk2_colour_mapping_manual ;
	sw_mk2_colour_mapping_auto_checker_one ;
}

switch(FEAT_TRAINS, SELF, sw_mk2_colour_mapping_decider, colour_scheme_1){
	1 : sw_mk2_colour_mapping_manual_check ;
	palette_2cc(company_colour1, company_colour2) ;
}


//Purchase menu colour mapping

switch(FEAT_TRAINS, SELF, sw_mk2_purchase_colours, current_year){
	ERA_SECTORISATION  : palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE) ;
	ERA_PRIVATE_ONE : palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
	ERA_PRIVATE_TWO : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ;
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, SELF, sw_mk2_purchase_colours_2cc_check, colour_scheme_1){
	1 : sw_mk2_purchase_colours ;
	palette_2cc(company_colour1, company_colour2) ;
}

//Item

item (FEAT_TRAINS, item_Mk2, 9020){
	property{
		name						   : string(STR_NAME_MK2) ;
		misc_flags                     : bitmask(TRAIN_FLAG_SPRITE_STACK, TRAIN_FLAG_2CC) ;
		sprite_id                      : SPRITE_ID_NEW_TRAIN ;
		length                         : 8 ;
		climates_available             : ALL_CLIMATES ;
		introduction_date              : date( 1963, 1, 1) ;
		model_life                     : 45 ;
		vehicle_life                   : 50 ;
		cost_factor                    : 65 ;
		running_cost_base              : RUNNING_COST_NONE ;
		running_cost_factor            : 38 ;
		refit_cost                     : 0 ;
		refittable_cargo_classes       : CC_PASSENGERS ;
		cargo_allow_refit              : [PASS, TOUR] ;
		cargo_capacity                 : 60 ;
		loading_speed                  : 16 ;
		track_type                     : RAIL ;
		speed                          : 100 mph ;
		power                          : 0 hp ;
		weight                         : 34 ton ;
	}
	graphics{
		default                 : sw_mk2_default ;
		purchase                : sw_mk2_purchase ;
		purchase_colour_mapping : sw_mk2_purchase_colours_2cc_check ;
		cargo_subtype_text      : sw_mk2_refit_text_decider ;
		colour_mapping          : sw_mk2_colour_mapping_decider ;
	}
}



//Mark 2 Brake










switch(FEAT_TRAINS, SELF, sw_mk2_bso_gfx_auto, current_year){
	ERA_BRBLUE         : gfx_mk2_brblue_f_bso ;
    ERA_PRIVATE_ONE : gfx_mk2_virgin_f_bso ;
    ERA_PRIVATE_TWO : gfx_mk2_directrail_f_bso ;
	gfx_mk2_intercity_f_bso ;
}



//Manual livery decider

switch(FEAT_TRAINS, SELF, sw_mk2_bso_gfx_manual, cargo_subtype){
	1 : gfx_mk2_brblue_f_bso ;
	2..3 : gfx_mk2_intercity_f_bso ;
	4 : gfx_mk2_regional_a_bso_reverse ;
	5 : gfx_mk2_nse_a_bso_reverse ;
	6 : gfx_mk2_virgin_f_bso ;
	7 : gfx_mk2_directrail_f_bso ;
	9 : gfx_mk2_brblue_f_bso;
	gfx_mk2_brblue_a_bso_reverse ;
}

switch(FEAT_TRAINS, SELF, sw_mk2_bso_livery_choice_decider, livery_choice){
	1 : sw_mk2_bso_gfx_manual ; 
	sw_mk2_bso_gfx_auto ;
}




//Purchase sprite decider
switch(FEAT_TRAINS, SELF, sw_mk2_bso_purchase, current_year){
	ERA_SECTORISATION  : gfx_mk2_bso_purchase_regional ;
	ERA_PRIVATE_ONE : gfx_mk2_bso_purchase_virgin ;
	ERA_PRIVATE_TWO : gfx_mk2_bso_purchase_directrail ;
	gfx_mk2_bso_purchase_brblue ;
}

//Colours

switch(FEAT_TRAINS, SELF, sw_mk2_bso_colour_mapping_auto, current_year){
	ERA_BRBLUE         : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
    1971..1978     : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
    ERA_PRIVATE_ONE : palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
    ERA_PRIVATE_TWO : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ;
	palette_2cc(COLOUR_GREY, COLOUR_RED) ;
}

switch(FEAT_TRAINS, SELF, sw_mk2_bso_colour_mapping_manual, cargo_subtype){
	2 : palette_2cc(COLOUR_GREY, COLOUR_RED) ;
	3 : palette_2cc(COLOUR_GREY, COLOUR_DARK_BLUE) ;
	4 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE) ;
	5 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_RED) ;
	6 : palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
	7 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN) ;
	8..9: palette_2cc(company_colour1, company_colour2) ;
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, SELF, sw_mk2_bso_colour_mapping_manual_check, livery_choice){
	1 : sw_mk2_bso_colour_mapping_manual ;
	sw_mk2_bso_colour_mapping_auto ;
}

switch(FEAT_TRAINS, SELF, sw_mk2_bso_colour_mapping_decider, colour_scheme_1){
	1 : sw_mk2_bso_colour_mapping_manual_check ;
	palette_2cc(company_colour1, company_colour2) ;
}


//Cargo refit text
switch(FEAT_TRAINS, SELF, sw_mk2_bso_refit_text, cargo_subtype){
	0 : return(string(STR_LIVERY_BRBLUE_SUBTYPE,string(STR_NAME_MK2A))) ;
	1 : return(string(STR_LIVERY_BRBLUE_SUBTYPE,string(STR_NAME_MK2F))) ;
	2 : string(STR_LIVERY_INTERCITY) ;
	3 : string(STR_LIVERY_BR_SCOTRAIL) ;
	4 : string(STR_LIVERY_REG_RAILWAYS) ;
	5 : string(STR_LIVERY_NSE) ;
	6 : string(STR_LIVERY_VIRGIN_TRAINS) ;
	7 : string(STR_LIVERY_DRS) ;
	8 : string(STR_LIVER_COMP_COLOURS_A);
	9 : string(STR_LIVER_COMP_COLOURS_F);
	return CB_RESULT_NO_TEXT ;
}

switch(FEAT_TRAINS, SELF, sw_mk2_bso_refit_text_decider, livery_choice){
	1 : sw_mk2_bso_refit_text ;
	return CB_RESULT_NO_TEXT ;
}


item (FEAT_TRAINS, item_Mk2_brake, 9021){
	property{
		name						   : string(STR_NAME_MK2_BRAKE) ;
		misc_flags                     : bitmask(TRAIN_FLAG_2CC) ;
		sprite_id                      : SPRITE_ID_NEW_TRAIN ;
		length                         : 8 ;
		climates_available             : ALL_CLIMATES ;
		introduction_date              : date( 1963, 1, 1) ;
		model_life                     : 45 ;
		vehicle_life                   : 50 ;
		cost_factor                    : 60 ;
		running_cost_base              : RUNNING_COST_NONE ;
		running_cost_factor            : 34 ;
		refit_cost                     : 0 ;
		refittable_cargo_classes       : CC_PASSENGERS ;
		cargo_allow_refit              : [PASS, TOUR] ;
		cargo_capacity                 : 50 ;
		loading_speed                  : 10 ;
		track_type                     : RAIL ;
		speed                          : 100 mph ;
		power                          : 0 hp ;
		weight                         : 34 ton ;
        variant_group:					9020;
	}
	graphics{
		default                 : sw_mk2_bso_livery_choice_decider ;
		purchase                : sw_mk2_bso_purchase ;
		purchase_colour_mapping : sw_mk2_purchase_colours_2cc_check ;
		cargo_subtype_text      : sw_mk2_bso_refit_text_decider ;
		colour_mapping          : sw_mk2_bso_colour_mapping_decider ;
	}
}

if (vehicle_selection < 2){
item (FEAT_TRAINS, item_Mk2_brake, 9021){
	property{
		climates_available             : NO_CLIMATE ;
	}
}
}
