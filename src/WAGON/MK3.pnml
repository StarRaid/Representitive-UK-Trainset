//Mark 3 Carriage

/*

Liveries done:
BR Blue
Intercity
Virgin
Midland Mainline (Blue)
EMT
First Fag
First Barbie
FGW
GWR

Liveries to do:
Scotrail
ATW
One
Virgin Pretendolino
Greater Anglia
Network Rail
Chiltern Railways

*/

//Spritesets

spriteset(gfx_mk3_generic             , "gfx/WAGON/mark_3.png"){ template_Mk2_single(  0,  0) }
spriteset(gfx_mk3_brblue              , "gfx/WAGON/mark_3.png"){ template_Mk2_single(  0, 25) }
spriteset(gfx_mk3_intercity           , "gfx/WAGON/mark_3.png"){ template_Mk2_single(  0, 50) }
spriteset(gfx_mk3_virgin              , "gfx/WAGON/mark_3.png"){ template_Mk2_single(  0, 75) }
spriteset(gfx_mk3_first_fag           , "gfx/WAGON/mark_3.png"){ template_Mk2_single(  0,100) }
spriteset(gfx_mk3_mml                 , "gfx/WAGON/mark_3.png"){ template_Mk2_single(  0,125) }
spriteset(gfx_mk3_first_barbie        , "gfx/WAGON/mark_3.png"){ template_Mk2_single(  0,150) }
spriteset(gfx_mk3_fgw                 , "gfx/WAGON/mark_3.png"){ template_Mk2_single(  0,175) }
spriteset(gfx_mk3_gwr                 , "gfx/WAGON/mark_3.png"){ template_Mk2_single(  0,200) }
spriteset(gfx_mk3_emt                 , "gfx/WAGON/mark_3.png"){ template_Mk2_single(  0,225) }
spriteset(gfx_mk3_atw_exec            , "gfx/WAGON/mark_3.png"){ template_Mk2_single(  0,250) }
spriteset(gfx_mk3_chiltern            , "gfx/WAGON/mark_3.png"){ template_Mk2_single(  0,275) }
spriteset(gfx_mk3_gner                , "gfx/WAGON/mark_3.png"){ template_Mk2_single(  0,325) }
spriteset(gfx_mk3_virgin_p            , "gfx/WAGON/mark_3.png"){ template_Mk2_single(  0,350) }
spriteset(gfx_mk3_first_scotrail      , "gfx/WAGON/mark_3.png"){ template_Mk2_single(170,150) }
spriteset(gfx_mk3_xc                  , "gfx/WAGON/mark_3.png"){ template_Mk2_single(  0,375) }
spriteset(gfx_mk3_vtec_front		  , "gfx/WAGON/mark_3.png"){ template_Mk2_double		(0,400) }
spriteset(gfx_mk3_vtec_back			  , "gfx/WAGON/mark_3.png"){ template_Mk2_double_reverse(0,400) }
spriteset(gfx_mk3_vtec_buffet		  , "gfx/WAGON/mark_3.png"){ template_Mk2_double		(179,400) }
spriteset(gfx_mk3_vtec_buffet_reverse , "gfx/WAGON/mark_3.png"){ template_Mk2_double_reverse(179,400) }

spriteset(gfx_mk3_purchase_brblue     , "gfx/WAGON/mark_3.png"){ template_purchase  ( 89, 25) }
spriteset(gfx_mk3_purchase_intercity  , "gfx/WAGON/mark_3.png"){ template_purchase  ( 89, 50) }
spriteset(gfx_mk3_purchase_virgin     , "gfx/WAGON/mark_3.png"){ template_purchase  ( 89, 75) }
spriteset(gfx_mk3_purchase_mml        , "gfx/WAGON/mark_3.png"){ template_purchase  ( 89,125) }
spriteset(gfx_mk3_purchase_fgw        , "gfx/WAGON/mark_3.png"){ template_purchase  ( 89,175) }
spriteset(gfx_mk3_purchase_emt        , "gfx/WAGON/mark_3.png"){ template_purchase  ( 89,225) }

spriteset(gfx_mk3_windows_normal      , "gfx/WAGON/mark_3.png"){ template_Mk2_single(180, 25)         }
spriteset(gfx_mk3_windows_sleeper     , "gfx/WAGON/mark_3.png"){ template_Mk2_double(180, 75)         }
spriteset(gfx_mk3_windows_sleeper_rev , "gfx/WAGON/mark_3.png"){ template_Mk2_double_reverse(180, 75) }
spriteset(gfx_mk3_windows_buffet      , "gfx/WAGON/mark_3.png"){ template_Mk2_double(180, 50)         }
spriteset(gfx_mk3_windows_buffet_rev  , "gfx/WAGON/mark_3.png"){ template_Mk2_double_reverse(180, 50) }

//Graphics switches

//VTEC switches

switch(FEAT_TRAINS, SELF, sw_mk3_gfx_vtec_pos_check_front, position_in_vehid_chain){
	0..1: gfx_mk3_vtec_front	;
	2	: gfx_mk3_vtec_buffet_reverse	;
	gfx_mk3_vtec_back ;
}

switch(FEAT_TRAINS, SELF, sw_mk3_gfx_vtec_pos_check_back, position_in_vehid_chain_from_end){
	0..1: gfx_mk3_vtec_back	;
	2	: gfx_mk3_vtec_buffet	;
	gfx_mk3_vtec_front ;
}

switch(FEAT_TRAINS, PARENT, sw_mk3_gfx_vtec_rev_check, vehicle_is_reversed){
	1 : sw_mk3_gfx_vtec_pos_check_back ;
	sw_mk3_gfx_vtec_pos_check_front ;
}


//Automatic liveries

switch(FEAT_TRAINS, SELF, sw_mk3_gfx_auto, current_year){
	ERA_BRTOPS        : gfx_mk3_brblue ;
	ERA_SECTORISATION : gfx_mk3_intercity ;
	2005..2009        : gfx_mk3_mml ;
	2010..2016        : gfx_mk3_emt ;
	2017..4096		  : gfx_mk3_gwr ;
	gfx_mk3_virgin ;
}


//Manual liveries

switch(FEAT_TRAINS, SELF, sw_mk3_gfx_manual, cargo_subtype){
	0      : gfx_mk3_brblue ;
	1      : gfx_mk3_intercity ;
	3      : gfx_mk3_mml ;
	4      : gfx_mk3_emt ;
	5..6   : gfx_mk3_first_fag ;
	7..8   : gfx_mk3_first_barbie ;
	9..10  : gfx_mk3_fgw ;
	11..12 : gfx_mk3_gwr ;
	13     : gfx_mk3_first_scotrail ;
	14     : gfx_mk3_generic ;
	15     : gfx_mk3_atw_exec ;
	16     : gfx_mk3_chiltern ;
	17     : gfx_mk3_intercity ;
	18..19 : gfx_mk3_generic ;
	20     : gfx_mk3_gner ;
	21     : gfx_mk3_virgin_p ;
	22     : sw_mk3_gfx_vtec_rev_check ;
	23     : gfx_mk3_xc ;
	gfx_mk3_virgin ;
}

switch(FEAT_TRAINS, SELF, sw_mk3_gfx_manual_check, livery_choice){
	1 : sw_mk3_gfx_manual ;
	sw_mk3_gfx_auto ;
}


/*Windows switches

switch(FEAT_TRAINS, SELF, sw_mk3_sleeper_pos_check_rev, (position_in_consist - 1) > (num_vehs_in_consist / 2)){
	1 : gfx_mk3_windows_sleeper_rev ;
	gfx_mk3_windows_normal ;
}

switch(FEAT_TRAINS, SELF, sw_mk3_sleeper_pos_check, (position_in_consist - 1) <= (num_vehs_in_consist / 2)){
	1 : gfx_mk3_windows_sleeper ;
	gfx_mk3_windows_normal ;
}

switch(FEAT_TRAINS, PARENT, sw_mk3_sleeper_rev_check, vehicle_is_reversed){
	1 : sw_mk3_sleeper_pos_check_rev ;
	sw_mk3_sleeper_pos_check ;
}*/

switch(FEAT_TRAINS, SELF, sw_mk3_sleeper_manual, cargo_subtype){
	6      : gfx_mk3_windows_sleeper ;
	8      : gfx_mk3_windows_sleeper ;
	10     : gfx_mk3_windows_sleeper ;
	12..13 : gfx_mk3_windows_sleeper ;
	17     : gfx_mk3_windows_sleeper ;
	gfx_mk3_windows_normal ;
}

switch(FEAT_TRAINS, SELF, sw_mk3_sleeper_manual_check, livery_choice){
	1 : sw_mk3_sleeper_manual ;
	gfx_mk3_windows_normal ;
}

switch(FEAT_TRAINS, SELF, sw_mk3_gfx_window, position_in_vehid_chain ){
	2 : gfx_mk3_windows_buffet ;
	sw_mk3_sleeper_manual_check ;
}

switch(FEAT_TRAINS, SELF, sw_mk3_gfx_window_reverse, position_in_vehid_chain_from_end ){
	2 : gfx_mk3_windows_buffet_rev ;
	sw_mk3_sleeper_manual_check ;
}

switch(FEAT_TRAINS, PARENT, sw_mk3_gfx_window_reverse_check, vehicle_is_reversed){
	1 : sw_mk3_gfx_window_reverse ;
	sw_mk3_gfx_window ;
}


//Sprite stacking

switch(FEAT_TRAINS, SELF, sw_mk3_sprite_stack, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]){
	1 : sw_mk3_gfx_manual_check ;
	2 : sw_mk3_gfx_window_reverse_check ;
}


//Refit text

switch(FEAT_TRAINS, SELF, sw_mk3_refit_text, cargo_subtype){
	0  : string(STR_LIVERY_BRBLUE) ;
	1  : string(STR_LIVERY_INTERCITY) ;
	2  : string(STR_LIVERY_VIRGIN_TRAINS) ;
	3  : string(STR_LIVERY_MIDLAND_MAINLINE_BLUE) ;
	4  : string(STR_LIVERY_EMT) ;
	5  : string(STR_LIVERY_FIRST_FAG, string(STR_LIVERY_EMPTY)) ;
	6  : string(STR_LIVERY_FIRST_FAG, string(STR_LIVERY_SLEEPER)) ;
	7  : string(STR_LIVERY_FIRST_BARBIE, string(STR_LIVERY_EMPTY)) ;
	8  : string(STR_LIVERY_FIRST_BARBIE, string(STR_LIVERY_SLEEPER)) ;
	9  : string(STR_LIVERY_FIRST_FGW, string(STR_LIVERY_EMPTY)) ;
	10 : string(STR_LIVERY_FIRST_FGW, string(STR_LIVERY_SLEEPER)) ;
	11 : string(STR_LIVERY_FIRST_GWR, string(STR_LIVERY_EMPTY)) ;
	12 : string(STR_LIVERY_FIRST_GWR, string(STR_LIVERY_SLEEPER)) ;
	13 : string(STR_LIVERY_FIRST_SCOT_RAIL, string(STR_LIVERY_SLEEPER)) ;
	14 : string(STR_LIVERY_NETWORK_RAIL) ;
	15 : string(STR_LIVERY_ATW_EXEC) ;
	16 : string(STR_LIVERY_CHILTERN) ;
	17 : string(STR_LIVERY_INTERCITY_SLEEPER) ;
	18 : string(STR_LIVERY_ONE_ANGLIA) ;
	19 : string(STR_LIVERY_ABELLIO_GREATER_ANGLIA) ;
	20 : string(STR_LIVERY_GNER) ;
	21 : string(STR_LIVERY_VIRGIN_TRAINS_FAKE) ;
	22 : string(STR_LIVERY_VTEC_LNER) ;
	23 : string(STR_LIVERY_ARRIVA_XC) ;
	CB_RESULT_NO_TEXT ;
}

switch(FEAT_TRAINS, SELF, sw_mk3_refit_text_check, livery_choice){
	1 : sw_mk3_refit_text ;
	CB_RESULT_NO_TEXT ;
}


//Purchase sprite switches

switch(FEAT_TRAINS, SELF, sw_mk3_purchase, current_year){
	ERA_BRTOPS        : gfx_mk3_purchase_brblue ;
	ERA_SECTORISATION : gfx_mk3_purchase_intercity ;
	2005..2016        : gfx_mk3_purchase_mml ;
	gfx_mk3_purchase_virgin ;
}

switch(FEAT_TRAINS, SELF, sw_mk3_purchase_colours, current_year){
	ERA_BRTOPS        : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	ERA_SECTORISATION : palette_2cc(COLOUR_WHITE, COLOUR_RED) ;
	2005..2016        : palette_2cc(COLOUR_DARK_BLUE, COLOUR_GREY) ;
	palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, SELF, sw_mk3_purchase_colours_2cc_check, colour_scheme_1){
	1 : sw_mk3_purchase_colours ;
	palette_2cc(company_colour1, company_colour1) ;
}



//Colour mapping


switch(FEAT_TRAINS, SELF, sw_mk3_colours_auto, current_year){
	ERA_BRTOPS        : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	ERA_SECTORISATION : palette_2cc(COLOUR_WHITE, COLOUR_RED) ;
	2005..2009        : palette_2cc(COLOUR_DARK_BLUE, COLOUR_GREY) ;
	2010..2016        : palette_2cc(COLOUR_DARK_BLUE, COLOUR_RED) ;
	2017..4096		  : palette_2cc(COLOUR_DARK_GREEN, COLOUR_WHITE) ;
	palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, SELF, sw_mk3_colours_manual, cargo_subtype){
	0      : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;    //BR Blue
	1      : palette_2cc(COLOUR_WHITE, COLOUR_RED) ;          //Intercity
	3      : palette_2cc(COLOUR_DARK_BLUE, COLOUR_GREY) ;     //MML
	4      : palette_2cc(COLOUR_DARK_BLUE, COLOUR_RED) ;      //EMT
	5..6   : palette_2cc(COLOUR_PALE_GREEN, COLOUR_YELLOW) ;  //First Fag
	7..8   : palette_2cc(COLOUR_DARK_BLUE, COLOUR_YELLOW) ;   //First Barbie
	9..10  : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PINK) ;     //First Great Western
	11..12 : palette_2cc(COLOUR_DARK_GREEN, COLOUR_WHITE) ;   //GWR
	13     : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;    //First Barbie
	14     : palette_2cc(COLOUR_YELLOW, COLOUR_YELLOW) ;      //Network Rail
	15     : palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE) ;
	16     : palette_2cc(COLOUR_GREY, COLOUR_WHITE) ;
	17     : palette_2cc(COLOUR_WHITE, COLOUR_RED) ;          //Intercity
	18     : palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE) ;     //One Anglia
	19     : palette_2cc(COLOUR_WHITE, COLOUR_RED) ;          //Greater Anglia
	20     : palette_2cc(COLOUR_DARK_BLUE, COLOUR_RED) ;      //GNER
	23     : palette_2cc(COLOUR_PINK, COLOUR_RED) ;           //XC
	palette_2cc(COLOUR_RED, COLOUR_WHITE) ;                   //Virgin
}

switch(FEAT_TRAINS, SELF, sw_mk3_colours_manual_check, livery_choice){
	1 : sw_mk3_colours_manual ;
	sw_mk3_colours_auto ;
}

switch(FEAT_TRAINS, SELF, sw_mk3_colours_2cc_check, colour_scheme_1){
	1 : sw_mk3_colours_manual_check ;
	palette_2cc(company_colour1, company_colour2) ;
}



//Item

item (FEAT_TRAINS, item_mk3, 9030) {
	property {
		name: 							string(STR_NAME_Mk3);
		climates_available: 			ALL_CLIMATES;
		introduction_date:				date(1975, 1, 1);
		model_life:						VEHICLE_NEVER_EXPIRES ;
		retire_early:					1;
		vehicle_life:					50;
		reliability_decay:				7;
		refittable_cargo_classes:		CC_PASSENGERS;
		cargo_allow_refit:				[PASS, TOUR];
		loading_speed:					18;
		cost_factor:					38;
		running_cost_factor:			34;
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		speed:							125 mph;
		misc_flags:						bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		refit_cost:						0;
		track_type:						RAIL;
		ai_special_flag:				AI_FLAG_CARGO;
		power:							0 hp;
		running_cost_base:				RUNNING_COST_NONE;
		dual_headed:					0;
		default_cargo_type:				PASS;
		cargo_capacity:					80;
		weight:							33 ton;
		//tractive_effort_coefficient:	0.07;
		//air_drag_coefficient:			0.08;
		length:							8;
		bitmask_vehicle_info:			0;
		cargo_age_period:				185;
	}
	graphics {
		default                 : sw_mk3_sprite_stack ;
		purchase                : sw_mk3_purchase ;
		purchase_colour_mapping : sw_mk3_purchase_colours_2cc_check ;
		cargo_subtype_text      : sw_mk3_refit_text_check ;
		colour_mapping          : sw_mk3_colours_2cc_check ;
	}
}