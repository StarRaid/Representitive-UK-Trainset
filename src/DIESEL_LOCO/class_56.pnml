//Class 56

spriteset(gfx_56_brblue,				"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double(0,0)}
spriteset(gfx_56_brblue_rev,			"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double_reverse(0,0)}
spriteset(gfx_56_brlarge,				"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double(0,25)}
spriteset(gfx_56_brlarge_rev,			"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double_reverse(0,25)}
spriteset(gfx_56_railfreight,			"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double(0,50)}
spriteset(gfx_56_railfreight_rev,		"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double_reverse(0,50)}
spriteset(gfx_56_ews,					"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double(0,75)}
spriteset(gfx_56_ews_rev,				"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double_reverse(0,75)}
spriteset(gfx_56_colas,					"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double(0,100)}
spriteset(gfx_56_colas_rev,				"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double_reverse(0,100)}
spriteset(gfx_56_loadhaul,				"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double(0,125)}
spriteset(gfx_56_loadhaul_rev,			"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double_reverse(0,125)}
spriteset(gfx_56_fastline,				"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double(0,150)}
spriteset(gfx_56_fastline_rev,			"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double_reverse(0,150)}
spriteset(gfx_56_dcrgrey,				"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double(0,175)}
spriteset(gfx_56_dcrgrey_rev,			"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double_reverse(0,175)}
spriteset(gfx_56_dcrgreen,				"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double(0,200)}
spriteset(gfx_56_dcrgreen_rev,			"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double_reverse(0,200)}

spriteset(gfx_56_lights_back,			"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double(235,0)}
spriteset(gfx_56_lights_front,			"gfx/DIESEL_LOCO/class_56.png"){template_Mk2_double(235,25)}

spriteset(gfx_56_purch_brblue,			"gfx/DIESEL_LOCO/class_56.png"){template_purchase(178,150)}
spriteset(gfx_56_purch_brlarge,			"gfx/DIESEL_LOCO/class_56.png"){template_purchase(178,175)}
spriteset(gfx_56_purch_railfreight,		"gfx/DIESEL_LOCO/class_56.png"){template_purchase(178,50)}
spriteset(gfx_56_purch_ews,				"gfx/DIESEL_LOCO/class_56.png"){template_purchase(178,75)}
spriteset(gfx_56_purch_colas,			"gfx/DIESEL_LOCO/class_56.png"){template_purchase(178,100)}


//Graphics switches

switch(FEAT_TRAINS, PARENT, sw_56_gfx_rev_check_brblue, vehicle_is_reversed){ 1 : gfx_56_brblue_rev ; gfx_56_brblue ; }
switch(FEAT_TRAINS, PARENT, sw_56_gfx_rev_check_brlarge, vehicle_is_reversed){ 1 : gfx_56_brlarge_rev ; gfx_56_brlarge ; }
switch(FEAT_TRAINS, PARENT, sw_56_gfx_rev_check_railfreight, vehicle_is_reversed){ 1 : gfx_56_railfreight_rev ; gfx_56_railfreight ; }
switch(FEAT_TRAINS, PARENT, sw_56_gfx_rev_check_ews, vehicle_is_reversed){ 1 : gfx_56_ews_rev ; gfx_56_ews ; }
switch(FEAT_TRAINS, PARENT, sw_56_gfx_rev_check_colas, vehicle_is_reversed){ 1 : gfx_56_colas_rev ; gfx_56_colas ; }
switch(FEAT_TRAINS, PARENT, sw_56_gfx_rev_check_loadhaul, vehicle_is_reversed){ 1 : gfx_56_loadhaul_rev ; gfx_56_loadhaul ; }
switch(FEAT_TRAINS, PARENT, sw_56_gfx_rev_check_fastline, vehicle_is_reversed){ 1 : gfx_56_fastline_rev ; gfx_56_fastline ; }
switch(FEAT_TRAINS, PARENT, sw_56_gfx_rev_check_dcrgrey, vehicle_is_reversed){ 1 : gfx_56_dcrgrey_rev ; gfx_56_dcrgrey ; }
switch(FEAT_TRAINS, PARENT, sw_56_gfx_rev_check_dcrgreen, vehicle_is_reversed){ 1 : gfx_56_dcrgreen_rev ; gfx_56_dcrgreen ; }


//Liveries

switch(FEAT_TRAINS, SELF, sw_56_gfx_auto, current_year){
	1967..1981	: sw_56_gfx_rev_check_brblue ;
	1982..1986	: sw_56_gfx_rev_check_brlarge ;
	1987..1996	: sw_56_gfx_rev_check_railfreight ;
	1997..2008	: sw_56_gfx_rev_check_ews ;
	sw_56_gfx_rev_check_colas ;
}

switch(FEAT_TRAINS, SELF, sw_56_gfx_manual, cargo_subtype){
	0	: sw_56_gfx_rev_check_brblue ;
	3	: sw_56_gfx_rev_check_railfreight ;
	4	: sw_56_gfx_rev_check_ews ;
	5	: sw_56_gfx_rev_check_loadhaul ;
	6	: sw_56_gfx_rev_check_colas ;
	7	: sw_56_gfx_rev_check_fastline ;
	8	: sw_56_gfx_rev_check_dcrgrey ;
	9	: sw_56_gfx_rev_check_dcrgreen ;
	sw_56_gfx_rev_check_brlarge ;
}

switch(FEAT_TRAINS, SELF, sw_56_gfx_manual_check, livery_choice){
	1 : sw_56_gfx_manual ;
	sw_56_gfx_auto ;
}


//Lights

switch(FEAT_TRAINS, SELF, sw_56_gfx_lights_back, position_in_consist_from_end){ 0 : gfx_56_lights_back ; }
switch(FEAT_TRAINS, SELF, sw_56_gfx_lights_front, position_in_consist){ 0 : gfx_56_lights_front ; }

switch(FEAT_TRAINS, SELF, sw_56_gfx_spr_stack, SPR_STACK){
	0 : sw_56_gfx_manual_check ;
	1 : sw_56_gfx_lights_back ;
	2 : sw_56_gfx_lights_front ;
}



//Purchase sprite

switch(FEAT_TRAINS, SELF, sw_56_purch_gfx, current_year){
	1967..1981	: gfx_56_purch_brblue ;
	1982..1986	: gfx_56_purch_brlarge ;
	1987..1996	: gfx_56_purch_railfreight ;
	1997..2008	: gfx_56_purch_ews ;
	gfx_56_purch_colas ;
}



//Refit text

switch(FEAT_TRAINS, SELF, sw_56_refit_text, cargo_subtype){
	0	: string(STR_LIVERY_BRBLUE) ;
	1	: string(STR_LIVERY_BRBLUE_LOGO) ;
	2	: string(STR_LIVERY_BRRAILFREIGHT_LOGO) ;
	3	: string(STR_LIVERY_RAILFREIGHT, string(STR_LIVERY_EMPTY)) ;
	4	: string(STR_LIVERY_EWS) ;
	5	: string(STR_LIVERY_LOADHAUL) ;
	6	: string(STR_LIVERY_COLAS) ;
	7	: string(STR_LIVERY_FASTLINE) ;
	8	: string(STR_LIVERY_DCR_GREY) ;
	9	: string(STR_LIVERY_DCR_GREEN) ;
	CB_RESULT_NO_TEXT ;
}

switch(FEAT_TRAINS, SELF, sw_56_refit_text_check, livery_choice){
	1 : sw_56_refit_text ;
	CB_RESULT_NO_TEXT ;
}



//Colour mapping

switch(FEAT_TRAINS, SELF, sw_56_col_auto, current_year){
	1967..1981	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	1982..1986	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	1987..1996	: palette_2cc(COLOUR_WHITE, COLOUR_GREY) ;
	1997..2008	: palette_2cc(COLOUR_RED, COLOUR_YELLOW) ;
	palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
}

switch(FEAT_TRAINS, SELF, sw_56_col_manual, cargo_subtype){
	2	: palette_2cc(COLOUR_GREY, COLOUR_WHITE) ;
	3	: palette_2cc(COLOUR_WHITE, COLOUR_GREY) ;
	4	: palette_2cc(COLOUR_RED, COLOUR_YELLOW) ;
	5	: palette_2cc(COLOUR_GREY, COLOUR_ORANGE) ;
	6	: palette_2cc(COLOUR_YELLOW, COLOUR_ORANGE) ;
	7	: palette_2cc(COLOUR_GREY, COLOUR_YELLOW) ;
	8	: palette_2cc(COLOUR_GREY, COLOUR_RED) ;
	9	: palette_2cc(COLOUR_DARK_GREEN, COLOUR_RED) ;
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, SELF, sw_56_col_manual_check, livery_choice){
	1 : sw_56_col_manual ;
	sw_56_col_auto ;
}

switch(FEAT_TRAINS, SELF, sw_56_col_2cc_check, colour_scheme_1){
	1 : sw_56_col_manual_check ;
	palette_2cc(company_colour1, company_colour2) ;
}



//Item

item(FEAT_TRAINS, item_grid, 56){
	property{
		name                           : string(STR_NAME_BREL56) ;  
		climates_available             : ALL_CLIMATES ;
		introduction_date              : date(1976,1,1) ;
		refit_cost                     : 0 ;
		refittable_cargo_classes       : 0 ;
		cargo_allow_refit              : [MAIL, GOOD] ;
		cargo_capacity                 : 1 ;
		model_life                     : VEHICLE_NEVER_EXPIRES ;
		vehicle_life                   : 30 ;
		reliability_decay              : 15 ;
		cost_factor                    : 82 ;
		running_cost_base              : RUNNING_COST_DIESEL ;
		running_cost_factor            : 240 ;
		speed                          : 80 mph ;
		track_type                     : RAIL ;
		power                          : 2424 kW ;
		weight                         : 128 ton ;
		engine_class                   : ENGINE_CLASS_DIESEL ;
		tractive_effort_coefficient    : 0.21 ;
		air_drag_coefficient           : 0.0888888888888889 ;
		length                         : 8 ;
		misc_flags                     : bitmask(TRAIN_FLAG_SPRITE_STACK, TRAIN_FLAG_2CC) ;
		effect_spawn_model_and_powered : EFFECT_SPAWN_MODEL_DIESEL ;
		sprite_id                      : SPRITE_ID_NEW_TRAIN ;
		dual_headed                    : 0 ;
	}
	graphics{
		default                        : sw_56_gfx_spr_stack ;
		colour_mapping                 : sw_56_col_2cc_check ;
		purchase                       : sw_56_purch_gfx ;
		purchase_colour_mapping        : sw_56_col_auto ;
		sound_effect                   : sw_sfx_diesel_loco_emd ;
		create_effect                  : vfx_diesel_default ;
		cargo_subtype_text             : sw_56_refit_text_check ;
		purchase_cargo_capacity        : 0 ;
		cargo_capacity                 : return 0 ;
		can_attach_wagon               : sw_all_but_MU90;
		running_cost_factor            : return(current_speed == 0 ? 70 : 240) ;
		purchase_running_cost_factor : 240 ;
	}
}


if (alt_names == 1){
item (FEAT_TRAINS, item_grid, 56){
	property{
		name                           : string(STR_NAME_BREL56_ALT) ;
	}
}
}

if (vehicle_selection < 2){
item (FEAT_TRAINS, item_grid, 56){
	property{
		climates_available             : NO_CLIMATE ;
	}
}
}
