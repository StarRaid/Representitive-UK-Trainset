//Brush Type 2

//Spritesets

spriteset(gfx_brush2_generic_grey				, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_2(	0,	0)		}
spriteset(gfx_brush2_generic_grey_rev			, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_rev_2(	0,	0)	}
spriteset(gfx_brush2_brgreen					, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_2(	0,	25)		}
spriteset(gfx_brush2_brgreen_rev				, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_rev_2(	0,	25)	}
spriteset(gfx_brush2_brblue						, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_2(	0,	50)		}
spriteset(gfx_brush2_brblue_rev					, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_rev_2(	0,	50)	}
spriteset(gfx_brush2_brlargelogo				, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_2(	0,	75)		}
spriteset(gfx_brush2_brlargelogo_rev			, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_rev_2(	0,	75)	}
spriteset(gfx_brush2_regionalrail				, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_2(	0,	100)	}
spriteset(gfx_brush2_regionalrail_rev			, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_rev_2(	0,	100)}
spriteset(gfx_brush2_intercity					, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_2(	0,	125)	}
spriteset(gfx_brush2_intercity_rev				, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_rev_2(	0,	125)}
spriteset(gfx_brush2_railfreight				, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_2(	0,	150)	}
spriteset(gfx_brush2_railfreight_rev			, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_rev_2(	0,	150)}
spriteset(gfx_brush2_ews						, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_2(	0,	175)	}
spriteset(gfx_brush2_ews_rev					, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_rev_2(	0,	175)}
spriteset(gfx_brush2_modern						, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_2(	0,	200)	}
spriteset(gfx_brush2_modern_rev					, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_rev_2(	0,	200)}

spriteset(gfx_brush2_taillights					, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_2(	235,	0)	}
spriteset(gfx_brush2_headlights					, "gfx/DIESEL_LOCO/class_31.png")	{ template_7_8_2(	235,	25)	}

spriteset(gfx_brush2_purchase_brblue			, "gfx/DIESEL_LOCO/class_31.png")	{ template_purchase(	178,	50)}
spriteset(gfx_brush2_purchase_brgreen			, "gfx/DIESEL_LOCO/class_31.png")	{ template_purchase(	275,	75)	}
spriteset(gfx_brush2_purchase_brlargelogo		, "gfx/DIESEL_LOCO/class_31.png")	{ template_purchase(	178,	75)	}
spriteset(gfx_brush2_purchase_regional			, "gfx/DIESEL_LOCO/class_31.png")	{ template_purchase(	178,	100)}
spriteset(gfx_brush2_purchase_ews				, "gfx/DIESEL_LOCO/class_31.png")	{ template_purchase(	178,	175)}
spriteset(gfx_brush2_purchase_modern			, "gfx/DIESEL_LOCO/class_31.png")	{ template_purchase(	178,	200)}


//Graphics switches

//switch(FEAT_TRAINS, PARENT, sw_brush2_generic_grey_revcheck, vehicle_is_reversed)		{ 1 : gfx_brush2_generic_grey_rev;			gfx_brush2_generic_grey; }
switch(FEAT_TRAINS, PARENT, sw_brush2_brgreen_revcheck, vehicle_is_reversed)			{ 1 : gfx_brush2_brgreen_rev;				gfx_brush2_brgreen; }
switch(FEAT_TRAINS, PARENT, sw_brush2_brblue_revcheck, vehicle_is_reversed)				{ 1 : gfx_brush2_brblue_rev;				gfx_brush2_brblue; }
switch(FEAT_TRAINS, PARENT, sw_brush2_brlargelogo_revcheck, vehicle_is_reversed)		{ 1 : gfx_brush2_brlargelogo_rev;			gfx_brush2_brlargelogo; }
switch(FEAT_TRAINS, PARENT, sw_brush2_regionalrail_revcheck, vehicle_is_reversed)		{ 1 : gfx_brush2_regionalrail_rev;			gfx_brush2_regionalrail; }
switch(FEAT_TRAINS, PARENT, sw_brush2_intercity_revcheck, vehicle_is_reversed)			{ 1 : gfx_brush2_intercity_rev;			gfx_brush2_intercity; }
switch(FEAT_TRAINS, PARENT, sw_brush2_railfreight_revcheck, vehicle_is_reversed)		{ 1 : gfx_brush2_railfreight_rev;			gfx_brush2_railfreight; }
switch(FEAT_TRAINS, PARENT, sw_brush2_ews_revcheck, vehicle_is_reversed)				{ 1 : gfx_brush2_ews_rev;					gfx_brush2_ews; }
switch(FEAT_TRAINS, PARENT, sw_brush2_modern_revcheck, vehicle_is_reversed)				{ 1 : gfx_brush2_modern_rev;				gfx_brush2_modern; }


//Automatic

random_switch(FEAT_TRAINS, SELF, sw_brush2_gfx_sectors_pass, TRIGGER_VEHICLE_SERVICE){ 2 : sw_brush2_regionalrail_revcheck; 1 : sw_brush2_intercity_revcheck; }

switch(FEAT_TRAINS, SELF, sw_brush2_gfx_sectors, most_common_cargo_type){
	PASS	: sw_brush2_gfx_sectors_pass;
	sw_brush2_railfreight_revcheck;
}

switch(FEAT_TRAINS, SELF, sw_brush2_gfx_private_1, most_common_cargo_type){
	PASS	: sw_brush2_modern_revcheck;
	sw_brush2_ews_revcheck;
}

random_switch(FEAT_TRAINS, SELF, sw_brush2_gfx_private_2, TRIGGER_VEHICLE_SERVICE){
	5 : sw_brush2_modern_revcheck;
	3 : sw_brush2_brgreen_revcheck;
	2 : sw_brush2_brblue_revcheck;
	2 : sw_brush2_ews_revcheck;
	2 : sw_brush2_brlargelogo_revcheck;
	1 : sw_brush2_railfreight_revcheck;
	1 : sw_brush2_intercity_revcheck;
	1 : sw_brush2_regionalrail_revcheck;
}

switch(FEAT_TRAINS, SELF, sw_brush2_gfx_auto, date_of_last_service){
	DATE_BRCREST:		sw_brush2_brgreen_revcheck;
	DATE_BRCOMBINED:	sw_brush2_brblue_revcheck;
	DATE_SECTORISATION:	sw_brush2_gfx_sectors;
	DATE_PRIVATE_ONE:	sw_brush2_gfx_private_1;
	sw_brush2_gfx_private_2;
}


//Manual

switch(FEAT_TRAINS, SELF, sw_brush2_gfx_manual, cargo_subtype){
	0		: sw_brush2_brgreen_revcheck;
	1		: sw_brush2_brblue_revcheck;
	4		: sw_brush2_regionalrail_revcheck;
	5		: sw_brush2_intercity_revcheck;
	6..7	: sw_brush2_railfreight_revcheck;
	8		: sw_brush2_ews_revcheck;
	9..11	: sw_brush2_modern_revcheck;
	sw_brush2_brlargelogo_revcheck;
}


switch(FEAT_TRAINS, SELF, sw_brush2_gfx_manual_check, livery_choice){
	1 : sw_brush2_gfx_manual;
	sw_brush2_gfx_auto;
}


//Sprite stack

switch(FEAT_TRAINS, SELF, sw_brush2_headlight_check, position_in_consist){ 0 : gfx_brush2_headlights; }
switch(FEAT_TRAINS, SELF, sw_brush2_taillight_check, position_in_consist_from_end){ 0 : gfx_brush2_taillights; }


switch(FEAT_TRAINS, SELF, sw_brush2_gfx_sprstack, SPR_STACK){
	0 : sw_brush2_gfx_manual_check;
	1 : sw_brush2_headlight_check;
	2 : sw_brush2_taillight_check;
}




//Purchase sprites

switch(FEAT_TRAINS, SELF, sw_brush2_purchase_gfx, current_year){
	ERA_BRCREST	: gfx_brush2_purchase_brgreen;
	1967..1980	: gfx_brush2_purchase_brblue;
	1981..1986	: gfx_brush2_purchase_brlargelogo;
	1987..1996	: gfx_brush2_purchase_regional;
	1997..2005  : gfx_brush2_purchase_ews;
	gfx_brush2_purchase_modern;
}

switch(FEAT_TRAINS, SELF, sw_brush2_purchase_colours, current_year){
	ERA_BRCREST	: palette_2cc(COLOUR_DARK_GREEN, COLOUR_DARK_GREEN);
	1967..1986	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE);
	1987..1996	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE);
	1997..2005  : palette_2cc(COLOUR_RED, COLOUR_YELLOW);
	palette_2cc(COLOUR_YELLOW, COLOUR_YELLOW);
}

switch(FEAT_TRAINS, SELF, sw_brush2_purchase_colours_2cc_check, colour_scheme_1){
	1 : sw_brush2_purchase_colours;
	palette_2cc(company_colour1, company_colour2);
}



//Refit text

switch(FEAT_TRAINS, SELF, sw_brush2_refit_text, cargo_subtype){
	0		: string(STR_LIVERY_BRGREEN);
	1		: string(STR_LIVERY_BRBLUE);
	2		: string(STR_LIVERY_BRBLUE_LOGO);
	3		: string(STR_LIVERY_BRRAILFREIGHT_LOGO);
	4		: string(STR_LIVERY_REG_RAILWAYS);
	5		: string(STR_LIVERY_INTERCITY);
	6		: string(STR_LIVERY_RAILFREIGHT, string(STR_LIVERY_EMPTY));
	7		: string(STR_LIVERY_CIVIL_ENGINEERS);
	8		: string(STR_LIVERY_EWS);
	9		: string(STR_LIVERY_WESSEX_TRAINS);
	10		: string(STR_LIVERY_DCR_GREEN);
	11		: string(STR_LIVERY_NETWORK_RAIL);
	CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS, SELF, sw_brush2_refit_text_check, livery_choice){
	1 : sw_brush2_refit_text;
	CB_RESULT_NO_TEXT;
}



//Colour mapping


//Automatic

switch(FEAT_TRAINS, PARENT, sw_brush2_col_largelogo, most_common_cargo_type){
	PASS : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE);
	palette_2cc(COLOUR_GREY, COLOUR_WHITE);
}

random_switch(FEAT_TRAINS, SELF, sw_brush2_col_sectors_pass, TRIGGER_VEHICLE_SERVICE){
	2 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE);
	1 : palette_2cc(COLOUR_GREY, COLOUR_RED);
}

random_switch(FEAT_TRAINS, SELF, sw_brush2_col_railfreight_random, TRIGGER_VEHICLE_SERVICE){
	3 : palette_2cc(COLOUR_WHITE, COLOUR_GREY);
	1 : palette_2cc(COLOUR_GREY, COLOUR_YELLOW);
}


switch(FEAT_TRAINS, SELF, sw_brush2_col_sectors, most_common_cargo_type){
	PASS	: sw_brush2_col_sectors_pass;
	sw_brush2_col_railfreight_random;
}

switch(FEAT_TRAINS, SELF, sw_brush2_col_private_1, most_common_cargo_type){
	PASS	: palette_2cc(COLOUR_PINK, COLOUR_WHITE);
	palette_2cc(COLOUR_RED, COLOUR_YELLOW);
}


random_switch(FEAT_TRAINS, SELF, sw_brush2_col_modern_random, TRIGGER_VEHICLE_SERVICE){
	2 : palette_2cc(COLOUR_YELLOW, COLOUR_YELLOW);
	2 : palette_2cc(COLOUR_DARK_GREEN, COLOUR_DARK_GREEN);
	1 : palette_2cc(COLOUR_PINK, COLOUR_WHITE);
}

random_switch(FEAT_TRAINS, SELF, sw_brush2_col_private_2, TRIGGER_VEHICLE_SERVICE){
	dependent : sw_brush2_gfx_private_2;
	5 : sw_brush2_col_modern_random;
	3 : palette_2cc(COLOUR_DARK_GREEN, COLOUR_DARK_GREEN);
	2 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE);
	2 : palette_2cc(COLOUR_RED, COLOUR_YELLOW);
	2 : sw_brush2_col_largelogo;
	1 : sw_brush2_col_railfreight_random;
	1 : palette_2cc(COLOUR_GREY, COLOUR_RED);
	1 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE);
}



switch(FEAT_TRAINS, SELF, sw_brush2_col_auto, date_of_last_service){
	DATE_BRCREST:		palette_2cc(COLOUR_DARK_GREEN, COLOUR_DARK_GREEN);
	DATE_BRCOMBINED:	palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE);
	DATE_SECTORISATION:	sw_brush2_col_sectors;
	DATE_PRIVATE_ONE:	sw_brush2_col_private_1;
	sw_brush2_col_private_2;
}


//Manual

switch(FEAT_TRAINS, SELF, sw_brush2_col_manual, cargo_subtype){
	0		: palette_2cc(COLOUR_DARK_GREEN, COLOUR_DARK_GREEN);
	3		: palette_2cc(COLOUR_GREY, COLOUR_WHITE);
	4		: palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE);
	5		: palette_2cc(COLOUR_GREY, COLOUR_RED);
	6		: palette_2cc(COLOUR_WHITE, COLOUR_GREY);
	7		: palette_2cc(COLOUR_GREY, COLOUR_YELLOW);
	8		: palette_2cc(COLOUR_RED, COLOUR_YELLOW);
	9		: palette_2cc(COLOUR_PINK, COLOUR_WHITE);
	10		: palette_2cc(COLOUR_DARK_GREEN, COLOUR_DARK_GREEN);
	11		: palette_2cc(COLOUR_YELLOW, COLOUR_YELLOW);
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE);
}


switch(FEAT_TRAINS, SELF, sw_brush2_col_manual_check, livery_choice){
	1 : sw_brush2_col_manual;
	sw_brush2_col_auto;
}

switch(FEAT_TRAINS, SELF , sw_brush2_col_2cc_check, colour_scheme_1){
	1 : sw_brush2_col_manual_check;
	palette_2cc(company_colour1, company_colour2);
}


//Item

item(FEAT_TRAINS, item_brush2, 31){
	property{
		name                           : string(STR_NAME_BRUSH_2);  
		climates_available             : ALL_CLIMATES;
		introduction_date              : date(1957,1,1);
		refit_cost                     : 0;
		refittable_cargo_classes       : 0;
		cargo_allow_refit              : [MAIL, GOOD];
		cargo_capacity                 : 1;
		model_life                     : 35;
		vehicle_life                   : 42;
		reliability_decay              : 13;
		cost_factor                    : 45;
		running_cost_base              : RUNNING_COST_DIESEL;
		running_cost_factor            : 150;
		speed                          : 80 mph;
		track_type                     : RAIL;
		power                          : 1044 kW;
		weight                         : 108 ton;
		engine_class                   : ENGINE_CLASS_DIESEL;
		tractive_effort_coefficient    : 0.14722222222222222222222222222222;
		air_drag_coefficient           : 0.0888888888888889;
		length                         : 7;
		misc_flags                     : bitmask(TRAIN_FLAG_SPRITE_STACK, TRAIN_FLAG_2CC);
		effect_spawn_model_and_powered : EFFECT_SPAWN_MODEL_DIESEL;
		sprite_id                      : SPRITE_ID_NEW_TRAIN;
		dual_headed                    : 0;
	}
	graphics{
		default                        : sw_brush2_gfx_sprstack;
		colour_mapping                 : sw_brush2_col_2cc_check;
		purchase                       : sw_brush2_purchase_gfx;
		purchase_colour_mapping        : sw_brush2_purchase_colours_2cc_check;
		sound_effect                   : sw_sfx_diesel_loco_ee_2;
		create_effect                  : vfx_diesel_default;
		cargo_subtype_text             : sw_brush2_refit_text_check;
		purchase_cargo_capacity        : 0;
		cargo_capacity                 : return 0;
		can_attach_wagon               : sw_all_but_MU90;
		running_cost_factor            : return(current_speed == 0 ? 38 : 150);
		purchase_running_cost_factor : 150;
	}
}


if (alt_names == 1){
item (FEAT_TRAINS, item_brush2, 31){
	property{
		name                           : string(STR_NAME_BRUSH_2_ALT);
	}
}
}
