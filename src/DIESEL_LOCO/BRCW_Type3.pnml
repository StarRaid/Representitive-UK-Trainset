//BRCW Type 3

//Spritesets

spriteset(gfx_brcw3_brg,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,0)}
spriteset(gfx_brcw3_bgy_1,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,21)}
spriteset(gfx_brcw3_bgy_2,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,42)}
spriteset(gfx_brcw3_brb_1,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,63)}
spriteset(gfx_brcw3_brb_2,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,84)}
spriteset(gfx_brcw3_brb_3,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,105)}
spriteset(gfx_brcw3_brb_4,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,126)}
spriteset(gfx_brcw3_brb_5,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,147)}
spriteset(gfx_brcw3_brb_6,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,168)}
spriteset(gfx_brcw3_brb_7,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,189)}
spriteset(gfx_brcw3_brb_8,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,210)}
spriteset(gfx_brcw3_rf3g_,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,231)}
spriteset(gfx_brcw3_br_gg,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,252)}
spriteset(gfx_brcw3_dutch,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,273)}
spriteset(gfx_brcw3_nse__,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,294)}
spriteset(gfx_brcw3_drs,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,315)}
spriteset(gfx_brcw3_wcrc,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,336)}
spriteset(gfx_brcw3_ews,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,357)}
spriteset(gfx_brcw3_frag,		"gfx/DIESEL_LOCO/class_33.png"){template_7_8_2(0,378)}

spriteset(gfx_brcw3_brg_purchase,		"gfx/DIESEL_LOCO/class_33.png"){template_purchase(161,0)}
spriteset(gfx_brcw3_brb_purchase,		"gfx/DIESEL_LOCO/class_33.png"){template_purchase(161,84)}
spriteset(gfx_brcw3_nse_purchase,		"gfx/DIESEL_LOCO/class_33.png"){template_purchase(161,294)}
spriteset(gfx_brcw3_drs_purchase,		"gfx/DIESEL_LOCO/class_33.png"){template_purchase(161,315)}
spriteset(gfx_brcw3_wcrc_purchase,		"gfx/DIESEL_LOCO/class_33.png"){template_purchase(161,336)}

spriteset(gfx_brcw3_construction,		"gfx/FIXTURES/class_33_RF_sectors.png"){template_7_8_2(0,0)}
spriteset(gfx_brcw3_distribution,		"gfx/FIXTURES/class_33_RF_sectors.png"){template_7_8_2(0,21)}


//Graphics switches

random_switch(FEAT_TRAINS, SELF, sw_gfx_brcw3_bgy, TRIGGER_VEHICLE_SERVICE){
	8 : gfx_brcw3_bgy_1;
	3 : gfx_brcw3_bgy_2;
}

random_switch(FEAT_TRAINS, SELF, sw_gfx_brcw3_brb, TRIGGER_VEHICLE_SERVICE){
	4 : gfx_brcw3_brb_1;
	20 : gfx_brcw3_brb_2;
	1 : gfx_brcw3_brb_3;
	2 : gfx_brcw3_brb_4;
	3 : gfx_brcw3_brb_5;
	6 : gfx_brcw3_brb_6;
	3 : gfx_brcw3_brb_7;
	3 : gfx_brcw3_brb_8;
}

//Automatic

switch(FEAT_TRAINS, SELF, sw_brcw3_gfx_sectors, most_common_cargo_type){
	PASS	: gfx_brcw3_nse__;
	gfx_brcw3_rf3g_;
}

switch(FEAT_TRAINS, SELF, sw_brcw3_gfx_private, most_common_cargo_type){
	PASS	: gfx_brcw3_wcrc;
	gfx_brcw3_drs;
}

switch(FEAT_TRAINS, SELF, sw_brcw3_gfx_rf, most_common_cargo_type){
	GOOD : gfx_brcw3_distribution;
	FOOD : gfx_brcw3_distribution;
	MAIL : gfx_brcw3_distribution;
	VALU : gfx_brcw3_distribution;
	GOLD : gfx_brcw3_distribution;
	gfx_brcw3_construction;
}

switch(FEAT_TRAINS, SELF, sw_brcw3_gfx_sectors_auto_stack, most_common_cargo_type){
	PASS	: gfx_blank;
	sw_brcw3_gfx_rf;
}

switch(FEAT_TRAINS, SELF, sw_brcw3_gfx_auto, date_of_last_service){
	DATE_BRCREST: 		sw_gfx_brcw3_bgy;
	DATE_BRCOMBINED: 	sw_gfx_brcw3_brb;
	DATE_SECTORISATION: sw_brcw3_gfx_sectors;
	DATE_PRIVATISATION: sw_brcw3_gfx_private;
	gfx_brcw3_wcrc;
}

switch(FEAT_TRAINS, SELF, sw_brcw3_gfx_stack_auto, date_of_last_service){
	DATE_SECTORISATION	: sw_brcw3_gfx_sectors_auto_stack;
	gfx_blank;
}


//Manual

switch(FEAT_TRAINS, SELF, sw_brcw3_gfx_manual, cargo_subtype){
	0: gfx_brcw3_brg;
	1: sw_gfx_brcw3_bgy;
	2: sw_gfx_brcw3_brb;
	3..4: gfx_brcw3_rf3g_;
	5: gfx_brcw3_br_gg;
	6: gfx_brcw3_dutch;
	7: gfx_brcw3_nse__;
	8: gfx_brcw3_ews;
	9: gfx_brcw3_frag;
	10: gfx_brcw3_drs;
	gfx_brcw3_wcrc;
}

switch(FEAT_TRAINS, SELF, sw_brcw3_gfx_manual_stack, cargo_subtype){
	3: gfx_brcw3_construction;
	4: gfx_brcw3_distribution;
	gfx_blank;
}


switch(FEAT_TRAINS, SELF, sw_brcw3_gfx_manual_check, livery_choice){
	1 : sw_brcw3_gfx_manual;
	sw_brcw3_gfx_auto;
}


switch(FEAT_TRAINS, SELF, sw_brcw3_gfx_manual_check_stack, livery_choice){
	1 : sw_brcw3_gfx_manual_stack;
	sw_brcw3_gfx_stack_auto;
}


//Sprite stack


switch(FEAT_TRAINS, SELF, sw_brcw3_gfx_sprstack, SPR_STACK){
	0 : sw_brcw3_gfx_manual_check;
	1 : sw_brcw3_gfx_manual_check_stack;
}

//Purchase sprites

switch(FEAT_TRAINS, SELF, sw_brcw3_purchase_gfx, current_year){
	ERA_BRCREST	: gfx_brcw3_brg_purchase;
	1967..1986	: gfx_brcw3_brb_purchase;
	1987..1996	: gfx_brcw3_nse_purchase;
	1997..2005  : gfx_brcw3_drs_purchase;
	gfx_brcw3_wcrc_purchase;
}

switch(FEAT_TRAINS, SELF, sw_brcw3_purchase_colours, current_year){
	ERA_BRCREST	: palette_2cc(COLOUR_DARK_GREEN, COLOUR_DARK_GREEN);
	1967..1986	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_DARK_BLUE);
	1987..1996	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_RED);
	1997..2005  : palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN);
	palette_2cc(COLOUR_RED, COLOUR_RED);
}

switch(FEAT_TRAINS, SELF, sw_brcw3_purchase_colours_2cc_check, colour_scheme_1){
	1 : sw_brcw3_purchase_colours;
	palette_2cc(company_colour1, company_colour2);
}

//Refit text

switch(FEAT_TRAINS, SELF, sw_brcw3_refit_text, cargo_subtype){
	0: string(STR_LIVERY_BRGREEN);
	1: string(STR_LIVERY_BRGREEN_YELLOW);
	2: string(STR_LIVERY_BRBLUE);
	3: string(STR_LIVERY_RAILFREIGHT);
	4: string(STR_LIVERY_RAILFREIGHT);
	5: string(STR_LIVERY_RAILFREIGHT2);
	6: string(STR_LIVERY_CIVIL_ENGINEERS);
	7: string(STR_LIVERY_NSE);
	8: string(STR_LIVERY_EWS);
	9: string(STR_LIVERY_FRAGONSET);
	10: string(STR_LIVERY_DRS);
	11: string(STR_LIVERY_WCRC);
	CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS, SELF, sw_brcw3_refit_text_check, livery_choice){
	1 : sw_brcw3_refit_text;
	CB_RESULT_NO_TEXT;
}



//Colour mapping


//Automatic


switch(FEAT_TRAINS, SELF, sw_brcw3_col_sectors, most_common_cargo_type){
	PASS	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_RED);
	palette_2cc(COLOUR_GREY, COLOUR_GREY);
}

switch(FEAT_TRAINS, SELF, sw_brcw3_col_private, most_common_cargo_type){
	PASS	: palette_2cc(COLOUR_RED, COLOUR_RED);
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN);
}

switch(FEAT_TRAINS, SELF, sw_brcw3_col_auto, date_of_last_service){
	DATE_BRCREST: 		palette_2cc(COLOUR_DARK_GREEN, COLOUR_DARK_GREEN);
	DATE_BRCOMBINED: 	palette_2cc(COLOUR_DARK_BLUE, COLOUR_DARK_BLUE);
	DATE_SECTORISATION: sw_brcw3_col_sectors;
	DATE_PRIVATISATION: sw_brcw3_col_private;
	palette_2cc(COLOUR_RED, COLOUR_RED);
}

//Manual

switch(FEAT_TRAINS, SELF, sw_brcw3_col_manual, cargo_subtype){
	0..1: palette_2cc(COLOUR_DARK_GREEN, COLOUR_DARK_GREEN);
	2: palette_2cc(COLOUR_DARK_BLUE, COLOUR_DARK_BLUE);
	3..4: palette_2cc(COLOUR_GREY, COLOUR_GREY);
	5: palette_2cc(COLOUR_GREY, COLOUR_GREY);
	6: palette_2cc(COLOUR_GREY, COLOUR_YELLOW);
	7: palette_2cc(COLOUR_DARK_BLUE, COLOUR_RED);
	8: palette_2cc(COLOUR_RED, COLOUR_YELLOW);
	9: palette_2cc(COLOUR_GREY, COLOUR_RED);
	10: palette_2cc(COLOUR_DARK_BLUE, COLOUR_PALE_GREEN); 
	palette_2cc(COLOUR_RED, COLOUR_RED);
}


switch(FEAT_TRAINS, SELF, sw_brcw3_col_manual_check, livery_choice){
	1 : sw_brcw3_col_manual;
	sw_brcw3_col_auto;
}

switch(FEAT_TRAINS, SELF , sw_brcw3_col_2cc_check, colour_scheme_1){
	1 : sw_brcw3_col_manual_check;
	palette_2cc(company_colour1, company_colour2);
}


//Item

item(FEAT_TRAINS, item_brcw3, 33){
	property{
		name                           : string(STR_NAME_BRCWTYPE3);  
		climates_available             : ALL_CLIMATES;
		introduction_date              : date(1960,1,1);
		refit_cost                     : 0;
		refittable_cargo_classes       : 0;
		cargo_allow_refit              : [MAIL, GOOD];
		cargo_capacity                 : 1;
		model_life                     : 35;
		vehicle_life                   : 42;
		reliability_decay              : 13;
		cost_factor                    : 45;
		running_cost_base              : RUNNING_COST_DIESEL;
		ai_special_flag:				AI_FLAG_PASSENGER ;
		running_cost_factor            : 125;
		speed                          : 85 mph;
		track_type                     : RAIL;
		power                          : 909 kW;
		weight                         : 76 ton;
		engine_class                   : ENGINE_CLASS_DIESEL;
		tractive_effort_coefficient    : 0.26;
		air_drag_coefficient           : 0.0888888888888889;
		length                         : 7;
		misc_flags                     : bitmask(TRAIN_FLAG_SPRITE_STACK, TRAIN_FLAG_2CC);
		effect_spawn_model_and_powered : EFFECT_SPAWN_MODEL_DIESEL;
		sprite_id                      : SPRITE_ID_NEW_TRAIN;
		dual_headed                    : 0;
	}
	graphics{
		default                        : sw_brcw3_gfx_sprstack;
		colour_mapping                 : sw_brcw3_col_2cc_check;
		purchase                       : sw_brcw3_purchase_gfx;
		purchase_colour_mapping        : sw_brcw3_purchase_colours_2cc_check;
		sound_effect                   : sw_sfx_diesel_loco_ee_2;
		create_effect                  : vfx_diesel_default;
		cargo_subtype_text             : sw_brcw3_refit_text_check;
		purchase_cargo_capacity        : 0;
		cargo_capacity                 : return 0;
		can_attach_wagon               : sw_all_but_MU90;
		running_cost_factor            : return(current_speed == 0 ? 38 : 125);
		purchase_running_cost_factor : 125;
	}
}

if (vehicle_selection < 3){
item (FEAT_TRAINS, item_brcw3, 33){
	property{
		climates_available             : NO_CLIMATE;
	}
}
}

if (alt_names == 1){
item (FEAT_TRAINS, item_brcw3, 33){
	property{
		name                           : string(STR_NAME_BRCWTYPE3_ALT);
	}
}
}
