//Class 700
spriteset(spriteset_700_tl_f, "gfx/EMU/class_717.png") {template_8_8_networker(0, 100)}
//Class 707
spriteset(spriteset_700_swt_f, "gfx/EMU/class_717.png") {template_8_8_networker(0, 125)}
spriteset(spriteset_700_swt_t, "gfx/EMU/class_717.png") {template_8_4_networker(179, 150)}
spriteset(spriteset_700_swt_p, "gfx/EMU/class_717.png") {template_8_8_networker(0, 150)}
spriteset(spriteset_700_swt_p_r, "gfx/EMU/class_717.png") {template_8_8_networker_reverse(0, 150)}
spriteset(spriteset_700_sec_f, "gfx/EMU/class_717.png") {template_8_8_networker(0, 175)}
spriteset(spriteset_700_sec_t, "gfx/EMU/class_717.png") {template_8_4_networker(179, 200)}
spriteset(spriteset_700_sec_p, "gfx/EMU/class_717.png") {template_8_8_networker(0, 200)}
spriteset(spriteset_700_sec_p_r, "gfx/EMU/class_717.png") {template_8_8_networker_reverse(0, 200)}
//Class 717
spriteset(spriteset_700_gn_f, "gfx/EMU/class_717.png") {template_8_8_networker(0, 25)}
//Common TSGN carriages
spriteset(spriteset_700_tsgn_t, "gfx/EMU/class_717.png") {template_8_4_networker(179, 0)}
spriteset(spriteset_700_tsgn_p, "gfx/EMU/class_717.png") {template_8_8_networker(0, 0)}
spriteset(spriteset_700_tsgn_p_r, "gfx/EMU/class_717.png") {template_8_8_networker_reverse(0, 0)}
//Fixtures
spriteset(spriteset_700_f_lights, "gfx/EMU/class_717.png") {template_8_8_networker(0, 50)}
spriteset(spriteset_700_r_lights, "gfx/EMU/class_717.png") {template_8_8_networker(0, 75)}
spriteset(spriteset_700_pan_up, "gfx/FIXTURES/class_375_lights.png") {template_8_8_networker(0, 50)}
spriteset(spriteset_700_pan_down, "gfx/FIXTURES/class_375_lights.png") {template_8_8_networker(0, 75)}
spriteset(spriteset_700_pan_up_r, "gfx/FIXTURES/class_375_lights.png") {template_8_8_networker_reverse(0, 50)}
spriteset(spriteset_700_pan_down_r, "gfx/FIXTURES/class_375_lights.png") {template_8_8_networker_reverse(0, 75)}
//Purchase
spriteset(spriteset_700_purchase, "gfx/EMU/class_717.png") {template_dualhead_purchase(179, 25)}
spriteset(spriteset_700_swt_purchase, "gfx/EMU/class_717.png") {template_dualhead_purchase(179, 50)}
spriteset(spriteset_700_gn_purchase, "gfx/EMU/class_717.png") {template_dualhead_purchase(179, 75)}

//Switches

switch(FEAT_TRAINS, PARENT, sw_700_front_auto, (count_veh_id(item_MU90)-(2*count_veh_id(item_700)))){
0: spriteset_700_gn_f;
1 .. 128: spriteset_700_tl_f;
spriteset_700_swt_f;
}

switch(FEAT_TRAINS, SELF, sw_700_purchase, current_year){
0 .. 2016: spriteset_700_purchase;
2017 .. 2018: spriteset_700_swt_purchase;
spriteset_700_gn_purchase;
}

switch(FEAT_TRAINS, SELF, sw_700_front_man, cargo_subtype){
0: spriteset_700_tl_f;
1: spriteset_700_swt_f;
2: spriteset_700_gn_f;
3: spriteset_700_sec_f;
}

switch(FEAT_TRAINS, SELF, sw_700_trailer_man, cargo_subtype){
0: spriteset_700_tsgn_t;
1: spriteset_700_swt_t;
2: spriteset_700_tsgn_t;
3: spriteset_700_sec_t;
}

switch(FEAT_TRAINS, SELF, sw_700_pan_man_f, cargo_subtype){
0: spriteset_700_tsgn_p;
1: spriteset_700_swt_p;
2: spriteset_700_tsgn_p;
3: spriteset_700_sec_p;
}

switch(FEAT_TRAINS, SELF, sw_700_pan_man_r, cargo_subtype){
0: spriteset_700_tsgn_p_r;
1: spriteset_700_swt_p_r;
2: spriteset_700_tsgn_p_r;
3: spriteset_700_sec_p_r;
}

switch(FEAT_TRAINS, PARENT, sw_700_pan_man, vehicle_is_reversed){
0: sw_700_pan_man_f;
sw_700_pan_man_r;
}

switch(FEAT_TRAINS, PARENT, sw_700_t_auto, (count_veh_id(item_MU90)-(2*count_veh_id(item_700)))){
0: spriteset_700_tsgn_t;
1 .. 128: spriteset_700_tsgn_t;
spriteset_700_swt_t;
}

switch(FEAT_TRAINS, PARENT, sw_700_p_auto_f, (count_veh_id(item_MU90)-(2*count_veh_id(item_700)))){
0: spriteset_700_tsgn_p;
1 .. 128: spriteset_700_tsgn_p;
spriteset_700_swt_p;
}

switch(FEAT_TRAINS, PARENT, sw_700_p_auto_r, (count_veh_id(item_MU90)-(2*count_veh_id(item_700)))){
0: spriteset_700_tsgn_p_r;
1 .. 128: spriteset_700_tsgn_p_r;
spriteset_700_swt_p_r;
}

switch(FEAT_TRAINS, PARENT, sw_700_p_auto, vehicle_is_reversed){
0: sw_700_p_auto_f;
sw_700_p_auto_r;
}

switch(FEAT_TRAINS, SELF, sw_700_livery_param, livery_choice){1 : sw_700_front_man ; sw_700_front_auto ;}
switch(FEAT_TRAINS, SELF, sw_700_p_livery_param, livery_choice){1 : sw_700_pan_man ; sw_700_p_auto ;}
switch(FEAT_TRAINS, SELF, sw_700_t_livery_param, livery_choice){1 : sw_700_trailer_man ; sw_700_t_auto ;}

switch(FEAT_TRAINS, SELF, sw_700_headlights_1, position_in_consist){
0: spriteset_700_f_lights;
gfx_blank;
}

switch(FEAT_TRAINS, SELF, sw_700_headlights, position_in_consist_from_end){
0: spriteset_700_r_lights;
sw_700_headlights_1;
}

switch(FEAT_TRAINS, PARENT, sw_700_pan_f, STANDARD_GAUGE_CHECK_OHLE_ONLY){
1: spriteset_700_pan_up;
spriteset_700_pan_down;
}

switch(FEAT_TRAINS, PARENT, sw_700_pan_r, STANDARD_GAUGE_CHECK_OHLE_ONLY){
1: spriteset_700_pan_up_r;
spriteset_700_pan_down_r;
}

switch(FEAT_TRAINS, SELF, sw_700_default, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]){
	0: return sw_700_livery_param;
	1: return sw_700_headlights;
}

switch(FEAT_TRAINS, SELF, sw_700_nd_livery_param_1, position_in_vehid_chain){
0: sw_700_p_livery_param;
sw_700_t_livery_param;
}

switch(FEAT_TRAINS, SELF, sw_700_nd_livery_param, position_in_vehid_chain_from_end){
0: sw_700_p_livery_param;
sw_700_nd_livery_param_1;
}

switch(FEAT_TRAINS, SELF, sw_700_ndf_pan_switch, position_in_vehid_chain){
0: sw_700_pan_f;
gfx_blank;
}

switch(FEAT_TRAINS, SELF, sw_700_nd_pan_switch, position_in_vehid_chain_from_end){
0: sw_700_pan_r;
sw_700_ndf_pan_switch;
}

switch(FEAT_TRAINS, SELF, sw_700_t_default, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]){
	0: return sw_700_nd_livery_param;
	1: return sw_700_nd_pan_switch;
}

switch(FEAT_TRAINS, SELF, sw_700_refit_text, cargo_subtype){
	0: string(STR_LIVERY_THAMESLINK_STR, string(STR_700));
	1: string(STR_LIVERY_SWT_STR, string(STR_707));
	2: string(STR_LIVERY_GN_STR, string(STR_717));
	3: string(STR_LIVERY_SE_REFURB_STR, string(STR_707));
	return CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS, SELF, sw_700_livery_decider, livery_choice){
	1: sw_700_refit_text;
	return CB_RESULT_NO_TEXT;
}

random_switch(FEAT_TRAINS, SELF, sw_700_tsgn_colour_mapping, TRIGGER_VEHICLE_SERVICE){1: palette_2cc(COLOUR_LIGHT_BLUE, company_colour2) ;}
random_switch(FEAT_TRAINS, SELF, sw_700_swt_colour_mapping, TRIGGER_VEHICLE_SERVICE){1: palette_2cc(COLOUR_RED, COLOUR_DARK_BLUE) ;}
random_switch(FEAT_TRAINS, SELF, sw_700_sec_colour_mapping, TRIGGER_VEHICLE_SERVICE){1: palette_2cc(COLOUR_DARK_BLUE, COLOUR_DARK_BLUE) ;}

switch(FEAT_TRAINS, SELF, sw_700_colour_auto, (count_veh_id(item_MU90)-(2*count_veh_id(item_700)))){
	0..128: sw_700_tsgn_colour_mapping;
	sw_700_swt_colour_mapping;
}

switch(FEAT_TRAINS, SELF, sw_700_colour_man, cargo_subtype){
	0: sw_700_tsgn_colour_mapping;
	1: sw_700_swt_colour_mapping;
	2: sw_700_tsgn_colour_mapping;
	3: sw_700_sec_colour_mapping;
}

switch(FEAT_TRAINS, SELF, sw_700_colour_livery_decider, livery_choice){
	1: sw_700_colour_man;
	sw_700_colour_auto;
}

switch(FEAT_TRAINS, SELF, sw_700_recolour, colour_scheme_1){
	1: sw_700_colour_livery_decider;
	palette_2cc(company_colour1, company_colour2);
}

switch(FEAT_TRAINS, SELF, sw_700_recolour_purchase_1, colour_scheme_1){
	0 .. 2016: sw_700_tsgn_colour_mapping;
	2017 .. 2018: sw_700_swt_colour_mapping;
	sw_700_tsgn_colour_mapping;
}

switch(FEAT_TRAINS, SELF, sw_700_recolour_purchase, colour_scheme_1){
	1: sw_700_recolour_purchase_1;
	palette_2cc(company_colour1, company_colour2);
}

switch(FEAT_TRAINS, SELF, sw_700_coupling_restrictions, vehicle_type_id) {
    9090: return CB_RESULT_ATTACH_ALLOW;
	700: return CB_RESULT_ATTACH_ALLOW;
	37 : return CB_RESULT_ATTACH_ALLOW;
    return string(STR_UNIQUE_NO_COUPLE, string(STR_NAME_700), string(STR_NAME_MU90));
}

item (FEAT_TRAINS, item_700, 700) {
	property {
		name: 							string(STR_NAME_700);
		climates_available: 			ALL_CLIMATES;
		introduction_date:				date(2015, 1, 1);
		model_life:						VEHICLE_NEVER_EXPIRES;
		retire_early:					1;
		vehicle_life:					50;
		reliability_decay:				7;
		refittable_cargo_classes:		CC_PASSENGERS;
		cargo_allow_refit:				[PASS, TOUR];
		loading_speed:					32;
		cost_factor:					75;
		running_cost_factor:			40;
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		speed:							100 mph;
		misc_flags:						bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_2CC, TRAIN_FLAG_SPRITE_STACK);
		refit_cost:						0;
		track_type:						SAAZ;
		ai_special_flag:				AI_FLAG_PASSENGER;
		power:							4425 hp;
		running_cost_base:				RUNNING_COST_ELECTRIC;
		dual_headed:					1;
		default_cargo_type:				PASS;
		cargo_capacity:					110;
		weight:							34 ton;
		engine_class:					ENGINE_CLASS_ELECTRIC;
		tractive_effort_coefficient:	0.2;
		air_drag_coefficient:			0.06;
		length:							8;
		effect_spawn_model_and_powered:	EFFECT_SPAWN_MODEL_ELECTRIC;
		bitmask_vehicle_info:			0;
	}
	graphics {
		can_attach_wagon:					sw_700_coupling_restrictions;
		default: 							sw_700_default;
		purchase:							sw_700_purchase;
		create_effect:						vfx_dual_voltage;
		colour_mapping:						sw_700_recolour;
		cargo_subtype_text:					sw_700_livery_decider;
		purchase_colour_mapping:			sw_700_recolour_purchase;
		sound_effect:						sw_sfx_emu_desiro;
		running_cost_factor:				return(current_speed == 0 ? 19 : 40) ;
		purchase_running_cost_factor : 40;
}
		livery_override (item_MU90) {
		cargo_capacity: 125;
		power: 200;
		default: sw_700_t_default;
		weight:	34;
		colour_mapping:	sw_700_recolour;
		cargo_subtype_text: sw_700_livery_decider;
}
}


if (alt_names == 1){
item (FEAT_TRAINS, item_700, 700){
	property{
		name                           : string(STR_NAME_700_ALT) ;
	}
}
}