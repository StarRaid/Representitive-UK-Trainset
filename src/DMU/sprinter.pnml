//SPRINTERS

spriteset(gfx_150_rr				, "gfx/DMU/150_2.png"){template_Mk2_double(0,0)}
spriteset(gfx_150_rr_mid			, "gfx/DMU/150_2.png"){template_Mk2_single(0,25)}
spriteset(gfx_150_mersey			, "gfx/DMU/150_2.png"){template_Mk2_double(0,50)}
spriteset(gfx_150_mersey_mid		, "gfx/DMU/150_2.png"){template_Mk2_single(0,75)}
spriteset(gfx_150_anglia			, "gfx/DMU/150_2.png"){template_Mk2_double(0,100)}
spriteset(gfx_150_anglia_mid		, "gfx/DMU/150_2.png"){template_Mk2_single(0,125)}
spriteset(gfx_150_central			, "gfx/DMU/150_2.png"){template_Mk2_double(0,150)}
spriteset(gfx_150_central_mid		, "gfx/DMU/150_2.png"){template_Mk2_single(0,175)}
spriteset(gfx_150_wessex			, "gfx/DMU/150_2.png"){template_Mk2_double(0,200)}
spriteset(gfx_150_wessex_mid		, "gfx/DMU/150_2.png"){template_Mk2_single(0,225)}
spriteset(gfx_150_base				, "gfx/DMU/150_2.png"){template_Mk2_double(0,250)}
spriteset(gfx_150_base_mid			, "gfx/DMU/150_2.png"){template_Mk2_single(0,275)}
spriteset(gfx_150_gwr				, "gfx/DMU/150_2.png"){template_Mk2_double(0,300)}
spriteset(gfx_150_gwr_mid			, "gfx/DMU/150_2.png"){template_Mk2_single(0,325)}
spriteset(gfx_150_fgw_plain			, "gfx/DMU/150_2.png"){template_Mk2_double(0,350)}
spriteset(gfx_150_fgw_plain_mid		, "gfx/DMU/150_2.png"){template_Mk2_single(0,375)}
spriteset(gfx_150_fgw				, "gfx/DMU/150_2.png"){template_Mk2_double(0,400)}
spriteset(gfx_150_fgw_mid			, "gfx/DMU/150_2.png"){template_Mk2_single(0,425)}
spriteset(gfx_150_northern_arr		, "gfx/DMU/150_2.png"){template_Mk2_double(0,450)}
spriteset(gfx_150_northern_arr_mid	, "gfx/DMU/150_2.png"){template_Mk2_single(0,475)}
spriteset(gfx_150_northern			, "gfx/DMU/150_2.png"){template_Mk2_double(0,500)}
spriteset(gfx_150_northern_mid		, "gfx/DMU/150_2.png"){template_Mk2_single(0,525)}
spriteset(gfx_150_arriva_exec		, "gfx/DMU/150_2.png"){template_Mk2_double(0,550)}
spriteset(gfx_150_arriva_exec_mid	, "gfx/DMU/150_2.png"){template_Mk2_single(0,575)}
spriteset(gfx_150_tfw				, "gfx/DMU/150_2.png"){template_Mk2_double(0,600)}
spriteset(gfx_150_tfw_mid			, "gfx/DMU/150_2.png"){template_Mk2_single(0,625)}

spriteset(gfx_150_purch_rr			, "gfx/DMU/150_2.png"){template_dualhead_purchase(90,25)}
spriteset(gfx_150_purch_central		, "gfx/DMU/150_2.png"){template_dualhead_purchase(90,175)}
spriteset(gfx_150_purch_wessex		, "gfx/DMU/150_2.png"){template_dualhead_purchase(90,225)}
spriteset(gfx_150_purch_gwr			, "gfx/DMU/150_2.png"){template_dualhead_purchase(90,325)}
spriteset(gfx_150_purch_fgw			, "gfx/DMU/150_2.png"){template_dualhead_purchase(90,425)}
spriteset(gfx_150_purch_fgw_plain	, "gfx/DMU/150_2.png"){template_dualhead_purchase(90,375)}
spriteset(gfx_150_purch_northern	, "gfx/DMU/150_2.png"){template_dualhead_purchase(90,525)}

spriteset(gfx_150_lights_front		, "gfx/DMU/150_2.png"){template_Mk2_double(179,0)}
spriteset(gfx_150_lights_back		, "gfx/DMU/150_2.png"){template_Mk2_double(179,25)}
spriteset(gfx_150_1_front			, "gfx/DMU/150_2.png"){template_Mk2_double(179,50)}


//Graphics switches

switch(FEAT_TRAINS, SELF, sw_150_gfx_idcheck_rr, vehicle_type_id){ 9030 : gfx_150_rr_mid ; gfx_150_rr ; }
switch(FEAT_TRAINS, SELF, sw_150_gfx_idcheck_mersey, vehicle_type_id){ 9030 : gfx_150_mersey_mid ; gfx_150_mersey ; }
switch(FEAT_TRAINS, SELF, sw_150_gfx_idcheck_anglia, vehicle_type_id){ 9030 : gfx_150_anglia_mid ; gfx_150_anglia ; }
switch(FEAT_TRAINS, SELF, sw_150_gfx_idcheck_central, vehicle_type_id){ 9030 : gfx_150_central_mid ; gfx_150_central ; }
switch(FEAT_TRAINS, SELF, sw_150_gfx_idcheck_wessex, vehicle_type_id){ 9030 : gfx_150_wessex_mid ; gfx_150_wessex ; }
switch(FEAT_TRAINS, SELF, sw_150_gfx_idcheck_base, vehicle_type_id){ 9030 : gfx_150_base_mid ; gfx_150_base ; }
switch(FEAT_TRAINS, SELF, sw_150_gfx_idcheck_gwr, vehicle_type_id){ 9030 : gfx_150_gwr_mid ; gfx_150_gwr ; }
switch(FEAT_TRAINS, SELF, sw_150_gfx_idcheck_fgw_plain, vehicle_type_id){ 9030 : gfx_150_fgw_plain_mid ; gfx_150_fgw_plain ; }
switch(FEAT_TRAINS, SELF, sw_150_gfx_idcheck_fgw, vehicle_type_id){ 9030 : gfx_150_fgw_mid ; gfx_150_fgw ; }
switch(FEAT_TRAINS, SELF, sw_150_gfx_idcheck_northern_arr, vehicle_type_id){ 9030 : gfx_150_northern_arr_mid ; gfx_150_northern_arr ; }
switch(FEAT_TRAINS, SELF, sw_150_gfx_idcheck_northern, vehicle_type_id){ 9030 : gfx_150_northern_mid ; gfx_150_northern ; }
switch(FEAT_TRAINS, SELF, sw_150_gfx_idcheck_arriva_exec, vehicle_type_id){ 9030 : gfx_150_arriva_exec_mid ; gfx_150_arriva_exec ; }
switch(FEAT_TRAINS, SELF, sw_150_gfx_idcheck_tfw, vehicle_type_id){ 9030 : gfx_150_tfw_mid ; gfx_150_tfw ; }



// 150/2

switch(FEAT_TRAINS, SELF, sw_150_2_gfx_auto, current_year){
	1980..1996	: sw_150_gfx_idcheck_rr ;
	1997..2005	: sw_150_gfx_idcheck_wessex ;
	2006..2016	: sw_150_gfx_idcheck_fgw ;
	sw_150_gfx_idcheck_gwr ;
}

switch(FEAT_TRAINS, SELF, sw_150_2_gfx_manual, cargo_subtype){
	0	: sw_150_gfx_idcheck_rr ;
	1	: sw_150_gfx_idcheck_central ;
	2	: sw_150_gfx_idcheck_northern_arr ;
	4	: sw_150_gfx_idcheck_fgw_plain ;
	5	: sw_150_gfx_idcheck_gwr ;
	6	: sw_150_gfx_idcheck_fgw ;
	7	: sw_150_gfx_idcheck_mersey ;
	8	: sw_150_gfx_idcheck_anglia ;
	9	: sw_150_gfx_idcheck_wessex ;
	11	: sw_150_gfx_idcheck_arriva_exec ;
	12	: sw_150_gfx_idcheck_tfw ;
	sw_150_gfx_idcheck_northern ;
}

switch(FEAT_TRAINS, SELF, sw_150_2_gfx_manual_check, livery_choice){
	1 : sw_150_2_gfx_manual ;
	sw_150_2_gfx_auto ;
}

// 150/1

switch(FEAT_TRAINS, SELF, sw_150_1_gfx_auto, current_year){
	1980..1996	: sw_150_gfx_idcheck_rr ;
	1997..2005	: sw_150_gfx_idcheck_central ;
	2006..2016	: sw_150_gfx_idcheck_fgw_plain ;
	sw_150_gfx_idcheck_northern ;
}

switch(FEAT_TRAINS, SELF, sw_150_1_gfx_manual, cargo_subtype){
	0	: sw_150_gfx_idcheck_rr ;
	1	: sw_150_gfx_idcheck_central ;
	2	: sw_150_gfx_idcheck_northern_arr ;
	4	: sw_150_gfx_idcheck_fgw_plain ;
	5	: sw_150_gfx_idcheck_gwr ;
	6	: sw_150_gfx_idcheck_base ;
	sw_150_gfx_idcheck_northern ;
}

switch(FEAT_TRAINS, SELF, sw_150_1_gfx_manual_check, livery_choice){
	1 : sw_150_1_gfx_manual ;
	sw_150_1_gfx_auto ;
}


//Sprite stack

switch(FEAT_TRAINS, SELF, sw_150_lights_check_back, position_in_consist_from_end){ 0 : gfx_150_lights_back ; }
switch(FEAT_TRAINS, SELF, sw_150_lights_check_front, position_in_consist){ 0 : gfx_150_lights_front ;  sw_150_lights_check_back ;}

switch(FEAT_TRAINS, SELF, sw_150_2_spr_stack, SPR_STACK){
	0 : sw_150_2_gfx_manual_check ;
	1 : sw_150_lights_check_front ;
}

switch(FEAT_TRAINS, SELF, sw_150_1_spr_stack, SPR_STACK){
	0 : sw_150_1_gfx_manual_check ;
	1 : gfx_150_1_front ;
	2 : sw_150_lights_check_front ;
}



//Purchase sprites

// 150/2

switch(FEAT_TRAINS, SELF, sw_150_2_purch_gfx, current_year){
	1980..1996	: gfx_150_purch_rr ;
	1997..2005	: gfx_150_purch_wessex ;
	2006..2016	: gfx_150_purch_fgw ;
	gfx_150_purch_gwr ;
}

// 150/1

switch(FEAT_TRAINS, SELF, sw_150_1_purch_gfx, current_year){
	1980..1996	: gfx_150_purch_rr ;
	1997..2005	: gfx_150_purch_central ;
	2006..2016	: gfx_150_purch_fgw_plain ;
	gfx_150_purch_northern ;
}


//Refit text

// 150/2

switch(FEAT_TRAINS, SELF, sw_150_2_refit_text, cargo_subtype){
	0	: string(STR_LIVERY_REG_RAILWAYS) ;
	1	: string(STR_LIVERY_CENTRAL) ;
	2	: string(STR_LIVERY_NORTHERN) ;
	3	: string(STR_LIVERY_NORTHERN_ARRIVA) ;
	4	: string(STR_LIVERY_FIRST_PLAIN) ;
	5	: string(STR_LIVERY_FIRST_GWR, string(STR_LIVERY_EMPTY)) ;
	6	: string(STR_LIVERY_FIRST_FGW, string(STR_LIVERY_EMPTY)) ;
	7	: string(STR_LIVERY_MERSEYRAIL) ;
	8	: string(STR_LIVERY_ANGLIA_RAIL) ;
	9	: string(STR_LIVERY_WESSEX_TRAINS) ;
	10	: string(STR_LIVERY_ATW) ;
	11	: string(STR_LIVERY_ATW_EXEC) ;
	12	: string(STR_LIVERY_TFW) ;
	CB_RESULT_NO_TEXT ;
}

switch(FEAT_TRAINS, SELF, sw_150_2_refit_text_check, livery_choice){
	1 : sw_150_2_refit_text ;
	CB_RESULT_NO_TEXT ;
}

// 150/1

switch(FEAT_TRAINS, SELF, sw_150_1_refit_text, cargo_subtype){
	0	: string(STR_LIVERY_REG_RAILWAYS) ;
	1	: string(STR_LIVERY_CENTRAL) ;
	2	: string(STR_LIVERY_NORTHERN) ;
	3	: string(STR_LIVERY_NORTHERN_ARRIVA) ;
	4	: string(STR_LIVERY_FIRST_PLAIN) ;
	5	: string(STR_LIVERY_FIRST_GWR, string(STR_LIVERY_EMPTY)) ;
	6	: string(STR_LIVERY_NETWORK_RAIL) ;
	CB_RESULT_NO_TEXT ;
}

switch(FEAT_TRAINS, SELF, sw_150_1_refit_text_check, livery_choice){
	1 : sw_150_1_refit_text ;
	CB_RESULT_NO_TEXT ;
}



//Colour mapping


// 150/2

switch(FEAT_TRAINS, SELF, sw_150_2_col_auto, current_year){
	1980..1996	: palette_1cc(COLOUR_DARK_BLUE) ;
	1997..2005	: palette_2cc(COLOUR_RED, COLOUR_PALE_GREEN) ;
	2006..2016	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_PINK) ;
	palette_2cc(COLOUR_DARK_GREEN, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, SELF, sw_150_2_col_manual, cargo_subtype){
	0	: palette_1cc(COLOUR_DARK_BLUE) ;
	1	: palette_1cc(COLOUR_DARK_BLUE) ;
	4	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_PINK) ;
	5	: palette_2cc(COLOUR_DARK_GREEN, COLOUR_WHITE) ;
	6	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_PINK) ;
	7	: palette_2cc(COLOUR_YELLOW, COLOUR_WHITE) ;
	8	: palette_2cc(COLOUR_BLUE, COLOUR_PALE_GREEN) ;
	9	: palette_2cc(COLOUR_RED, COLOUR_PALE_GREEN) ;
	10	: palette_2cc(COLOUR_WHITE, COLOUR_BLUE) ;
	11	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE) ;
	12	: palette_2cc(COLOUR_WHITE, COLOUR_RED) ;
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, SELF, sw_150_2_col_manual_check, livery_choice){
	1 : sw_150_2_col_manual ;
	sw_150_2_col_auto ;
}

switch(FEAT_TRAINS, SELF, sw_150_2_col_2cc_check, colour_scheme_1){
	1 : sw_150_2_col_manual_check ;
	palette_2cc(company_colour1, company_colour2) ;
}

// 150/1

switch(FEAT_TRAINS, SELF, sw_150_1_col_auto, current_year){
	1980..1996	: palette_1cc(COLOUR_DARK_BLUE) ;
	1997..2005	: palette_1cc(COLOUR_DARK_BLUE) ;
	2006..2016	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_PINK) ;
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, SELF, sw_150_1_col_manual, cargo_subtype){
	0	: palette_1cc(COLOUR_DARK_BLUE) ;
	1	: palette_1cc(COLOUR_DARK_BLUE) ;
	4	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_PINK) ;
	5	: palette_2cc(COLOUR_DARK_GREEN, COLOUR_WHITE) ;
	6	: palette_2cc(COLOUR_YELLOW, COLOUR_YELLOW) ;
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, SELF, sw_150_1_col_manual_check, livery_choice){
	1 : sw_150_1_col_manual ;
	sw_150_1_col_auto ;
}

switch(FEAT_TRAINS, SELF, sw_150_1_col_2cc_check, colour_scheme_1){
	1 : sw_150_1_col_manual_check ;
	palette_2cc(company_colour1, company_colour2) ;
}


//Items

switch(FEAT_TRAINS, SELF, sw_150_attach, vehicle_type_id){
	37   : return CB_RESULT_ATTACH_ALLOW  ;
	142  : return CB_RESULT_ATTACH_ALLOW  ;
	150  : return CB_RESULT_ATTACH_ALLOW  ;
	151  : return CB_RESULT_ATTACH_ALLOW  ;
	153  : return CB_RESULT_ATTACH_ALLOW  ;
	155  : return CB_RESULT_ATTACH_ALLOW  ;
	158  : return CB_RESULT_ATTACH_ALLOW  ;
	168	 : return CB_RESULT_ATTACH_ALLOW  ;
	9030 : return CB_RESULT_ATTACH_ALLOW  ;
	return string(STR_150_NO_COUPLE) ;
}


///150 /2

item(FEAT_TRAINS, item_sprinter, 150){
	property{
		name                           : string(STR_NAME_150_2) ;
		misc_flags                     : bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
		sprite_id                      : SPRITE_ID_NEW_TRAIN ;
		length                         : 8 ;
		climates_available             : ALL_CLIMATES ;
		introduction_date              : date(1984,1,1) ;
		model_life                     : 40 ;
		vehicle_life                   : 50 ;
		reliability_decay              : 6 ;
		cost_factor                    : 47 ;
		running_cost_base              : RUNNING_COST_DIESEL ;
		running_cost_factor            : 30 ;
		refit_cost                     : 0 ;
		refittable_cargo_classes       : CC_PASSENGERS ;
		cargo_allow_refit              : [PASS, TOUR] ;
		cargo_capacity                 : 85 ;
		loading_speed                  : 25 ;
		track_type                     : RAIL ;
		engine_class                   : ENGINE_CLASS_DIESEL ;
		speed                          : 75 mph ;
		power                          : 416 kW ;
		weight                         : 36 ton ;
		tractive_effort_coefficient    : 0.28 ;
		air_drag_coefficient           : 0.107 ;
		effect_spawn_model_and_powered : EFFECT_SPAWN_MODEL_DIESEL ;
		dual_headed                    : 1 ;
		ai_special_flag:				AI_FLAG_PASSENGER ;
	}
	graphics{
		default                 		: sw_150_2_spr_stack ;
		purchase                		: sw_150_2_purch_gfx ;
		sound_effect            		: sw_sfx_dmu_one ;
		create_effect           		: vfx_diesel_default ;
		colour_mapping          		: sw_150_2_col_2cc_check ;
		purchase_colour_mapping 		: sw_150_2_col_auto ;
		cargo_subtype_text      		: sw_150_2_refit_text_check ;
		can_attach_wagon        		: sw_150_attach ;
		running_cost_factor    			: return(current_speed == 0 ? 10 : 30) ;
		purchase_running_cost_factor	: 30 ;
	}
	livery_override (9030){
		default                     : sw_150_2_spr_stack ;
		colour_mapping              : sw_150_2_col_2cc_check ;
		cargo_capacity              : 85 ;
		power                       : 206 ;
		cargo_subtype_text          : sw_150_2_refit_text_check ;
		weight                      : 39 ;
		create_effect           	: vfx_diesel_default ;
	}
}


if (alt_names == 1){
item (FEAT_TRAINS, item_sprinter, 150){
	property{
		name                           : string(STR_NAME_150_2_ALT) ;
	}
}
}



//150 /1


item(FEAT_TRAINS, item_sprinter_1, 151){
	property{
		name                           : string(STR_NAME_150_1) ;
		misc_flags                     : bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
		sprite_id                      : SPRITE_ID_NEW_TRAIN ;
		length                         : 8 ;
		climates_available             : ALL_CLIMATES ;
		introduction_date              : date(1984,1,1) ;
		model_life                     : 40 ;
		vehicle_life                   : 50 ;
		reliability_decay              : 6 ;
		cost_factor                    : 47 ;
		running_cost_base              : RUNNING_COST_DIESEL ;
		running_cost_factor            : 30 ;
		refit_cost                     : 0 ;
		refittable_cargo_classes       : CC_PASSENGERS ;
		cargo_allow_refit              : [PASS, TOUR] ;
		cargo_capacity                 : 85 ;
		loading_speed                  : 25 ;
		track_type                     : RAIL ;
		engine_class                   : ENGINE_CLASS_DIESEL ;
		speed                          : 75 mph ;
		power                          : 416 kW ;
		weight                         : 36 ton ;
		tractive_effort_coefficient    : 0.23 ;
		air_drag_coefficient           : 0.107 ;
		effect_spawn_model_and_powered : EFFECT_SPAWN_MODEL_DIESEL ;
		dual_headed                    : 1 ;
		ai_special_flag:				AI_FLAG_PASSENGER ;
	}
	graphics{
		default                 		: sw_150_1_spr_stack ;
		purchase                		: sw_150_1_purch_gfx ;
		sound_effect            		: sw_sfx_dmu_one ;
		create_effect           		: vfx_diesel_default ;
		colour_mapping          		: sw_150_1_col_2cc_check ;
		purchase_colour_mapping 		: sw_150_1_col_auto ;
		cargo_subtype_text      		: sw_150_1_refit_text_check ;
		can_attach_wagon        		: sw_150_attach ;
		running_cost_factor    			: return(current_speed == 0 ? 10 : 30) ;
		purchase_running_cost_factor	: 30 ;
	}
	livery_override (9030){
		default                     : sw_150_1_spr_stack ;
		colour_mapping              : sw_150_1_col_2cc_check ;
		cargo_capacity              : 85 ;
		power                       : 100 ;
		cargo_subtype_text          : sw_150_1_refit_text_check ;
		weight                      : 39 ;
		effect_spawn_model_and_powered : EFFECT_SPAWN_MODEL_DIESEL ;
		create_effect           : vfx_diesel_default ;
	}
}


if (alt_names == 1){
item (FEAT_TRAINS, item_sprinter_1, 151){
	property{
		name                           : string(STR_NAME_150_1_ALT) ;
	}
}
}



if (vehicle_selection < 3){
item (FEAT_TRAINS, item_sprinter_1, 151){
	property{
		climates_available             : NO_CLIMATE ;
	}
}
}