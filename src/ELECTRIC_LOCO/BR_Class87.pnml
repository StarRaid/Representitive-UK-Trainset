//Class 87

//Spritesets

spriteset(gfx_br87_brblue              , "gfx/ELECTRIC_LOCO/class_87.png"){template_Mk2_double(0,0)}
spriteset(gfx_br87_brblue_reverse      , "gfx/ELECTRIC_LOCO/class_87.png"){template_Mk2_double_reverse(0,0)}
spriteset(gfx_br87_intercity           , "gfx/ELECTRIC_LOCO/class_87.png"){template_Mk2_double(0,25)}
spriteset(gfx_br87_intercity_reverse   , "gfx/ELECTRIC_LOCO/class_87.png"){template_Mk2_double_reverse(0,25)}
spriteset(gfx_br87_virgin              , "gfx/ELECTRIC_LOCO/class_87.png"){template_Mk2_double(0,50)}
spriteset(gfx_br87_virgin_reverse      , "gfx/ELECTRIC_LOCO/class_87.png"){template_Mk2_double_reverse(0,50)}
spriteset(gfx_br87_calsleeper          , "gfx/ELECTRIC_LOCO/class_87.png"){template_Mk2_double(0,75)}
spriteset(gfx_br87_calsleeper_reverse  , "gfx/ELECTRIC_LOCO/class_87.png"){template_Mk2_double_reverse(0,75)}
spriteset(gfx_br87_gbrf                , "gfx/ELECTRIC_LOCO/class_87.png"){template_Mk2_double(0,100)}
spriteset(gfx_br87_gbrf_reverse        , "gfx/ELECTRIC_LOCO/class_87.png"){template_Mk2_double_reverse(0,100)}

spriteset(gfx_br87_brblue_purchase     , "gfx/ELECTRIC_LOCO/class_87.png"){template_purchase(178,0)}
spriteset(gfx_br87_intercity_purchase  , "gfx/ELECTRIC_LOCO/class_87.png"){template_purchase(178,25)}
spriteset(gfx_br87_virgin_purchase     , "gfx/ELECTRIC_LOCO/class_87.png"){template_purchase(178,50)}
spriteset(gfx_br87_calsleeper_purchase , "gfx/ELECTRIC_LOCO/class_87.png"){template_purchase(178,75)}

spriteset(gfx_br87_headlight     , "gfx/FIXTURES/class_87_headlights.png"){template_Mk2_double(0,0)}
spriteset(gfx_br87_taillight     , "gfx/FIXTURES/class_87_headlights.png"){template_Mk2_double(0,25)}

//Switches

//Graphics

switch(FEAT_TRAINS, PARENT, sw_br87_gfx_brblue_reverse_check     , vehicle_is_reversed){ 1 : gfx_br87_brblue_reverse     ; gfx_br87_brblue ;}
switch(FEAT_TRAINS, PARENT, sw_br87_gfx_intercity_reverse_check  , vehicle_is_reversed){ 1 : gfx_br87_intercity_reverse  ; gfx_br87_intercity ;}
switch(FEAT_TRAINS, PARENT, sw_br87_gfx_virgin_reverse_check     , vehicle_is_reversed){ 1 : gfx_br87_virgin_reverse     ; gfx_br87_virgin ;}
switch(FEAT_TRAINS, PARENT, sw_br87_gfx_calsleeper_reverse_check , vehicle_is_reversed){ 1 : gfx_br87_calsleeper_reverse ; gfx_br87_calsleeper ;}
switch(FEAT_TRAINS, PARENT, sw_br87_gfx_gbrf_reverse_check       , vehicle_is_reversed){ 1 : gfx_br87_gbrf_reverse       ; gfx_br87_gbrf ;}

//Automatic liveries

switch(FEAT_TRAINS, PARENT, sw_br87_gfx_privatised, most_common_cargo_type){
	PASS : sw_br87_gfx_calsleeper_reverse_check ;
	sw_br87_gfx_gbrf_reverse_check ;
}

switch(FEAT_TRAINS, SELF, sw_br87_gfx_auto, build_year){
	ERA_BRTOPS : sw_br87_gfx_brblue_reverse_check ;
	ERA_SECTORISATION : sw_br87_gfx_intercity_reverse_check ;
	ERA_PRIVATE_TWO : sw_br87_gfx_privatised ;
	sw_br87_gfx_virgin_reverse_check ;
}

//Manual liveries

switch(FEAT_TRAINS, SELF, sw_br87_gfx_manual, cargo_subtype){
	0 : sw_br87_gfx_brblue_reverse_check ;
	1 : sw_br87_gfx_intercity_reverse_check ;
	2 : sw_br87_gfx_virgin_reverse_check ;
	3 : sw_br87_gfx_calsleeper_reverse_check ;
	4 : sw_br87_gfx_gbrf_reverse_check ;
	sw_br87_gfx_virgin_reverse_check ;
}


switch(FEAT_TRAINS, SELF, sw_br87_gfx_manual_check, livery_choice){
	1 : sw_br87_gfx_manual ;
	sw_br87_gfx_auto ;
}


//Sprite stacking

switch(FEAT_TRAINS, SELF, sw_br87_taillight_decider, position_in_consist_from_end){
	0 : gfx_br87_taillight ;
}

switch(FEAT_TRAINS, SELF, sw_br87_headlight_decider, position_in_consist){
	0 : gfx_br87_headlight ;
}

switch(FEAT_TRAINS, SELF, sw_br87_sprite_stack, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]){
	0 : sw_br87_gfx_manual_check ;
	1 : sw_br87_headlight_decider ;
	2 : sw_br87_taillight_decider ;
}


//Cargo refit text

switch(FEAT_TRAINS, SELF, sw_br87_refit_text, cargo_subtype){
	0 : string(STR_LIVERY_BRBLUE) ;
	1 : string(STR_LIVERY_INTERCITY) ;
	2 : string(STR_LIVERY_VIRGIN_TRAINS) ;
	3 : string(STR_LIVERY_CAL_SLEEPER) ;
	4 : string(STR_LIVERY_GBRF) ;
	CB_RESULT_NO_TEXT ;
}

switch(FEAT_TRAINS, SELF, sw_br87_refit_text_checker, livery_choice){
	1 : sw_br87_refit_text ;
	CB_RESULT_NO_TEXT ;
}


//Purchase sprite

switch(FEAT_TRAINS, SELF, sw_br87_purchase, current_year){
	ERA_BRTOPS     : gfx_br87_brblue_purchase ;  
	ERA_SECTORISATION  : gfx_br87_intercity_purchase ;  
	ERA_PRIVATE_TWO : gfx_br87_calsleeper_purchase ;
	gfx_br87_virgin_purchase ;
}

//Colour mapping


random_switch(FEAT_TRAINS, SELF, sw_br87_sectorised_colours, TRIGGER_VEHICLE_SERVICE){ 3 : palette_2cc(COLOUR_GREY, COLOUR_RED) ; 1 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_RED) ;}

switch(FEAT_TRAINS, SELF, sw_br87_sectorised_colours_dbso_check, count_veh_id(9025) > 0){
	1 : palette_2cc(COLOUR_GREY, COLOUR_RED) ;
	sw_br87_sectorised_colours ;
}

switch(FEAT_TRAINS, PARENT, sw_br87_privatised_colours, most_common_cargo_type){
	PASS : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_ORANGE) ;
}

switch(FEAT_TRAINS, SELF, sw_br87_colours_auto, build_year){
	ERA_BRTOPS     : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	ERA_SECTORISATION  : sw_br87_sectorised_colours_dbso_check ;
	ERA_PRIVATE_TWO : sw_br87_privatised_colours ;
	palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, SELF, sw_br87_colours_manual, cargo_subtype){
	0 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	1 : palette_2cc(COLOUR_GREY, COLOUR_RED) ;
	2 : palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
	3 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	4 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_ORANGE) ;
	palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, SELF, sw_br87_colours_manual_check, livery_choice){
	1 : sw_br87_colours_manual ;
	sw_br87_colours_auto ;
}

switch(FEAT_TRAINS, SELF, sw_br87_colours_2cc_check, colour_scheme_1){
	1 : sw_br87_colours_manual_check ;
	palette_2cc(company_colour1, company_colour2) ;
}


//Mark 3 override

//Graphics

switch(FEAT_TRAINS, SELF, sw_br87_mk3_override_gfx, current_year){
	2005..2014 : gfx_mk3_first_scotrail ;    //First Barbie
	sw_mk3_gfx_auto ;
}


//Windows deciders

switch(FEAT_TRAINS, SELF, sw_br87_mk3_override_windows, position_in_vehid_chain){
	2 : gfx_mk3_windows_buffet ;
	gfx_mk3_windows_sleeper ;
}

switch(FEAT_TRAINS, SELF, sw_br87_mk3_override_windows_rev, position_in_vehid_chain_from_end){
	2 : gfx_mk3_windows_buffet_rev ;
	gfx_mk3_windows_sleeper ;
}

switch(FEAT_TRAINS, PARENT, sw_br87_mk3_override_windows_rev_check, vehicle_is_reversed){
	1 : sw_br87_mk3_override_windows_rev ;
	sw_br87_mk3_override_windows ;
}

switch(FEAT_TRAINS, SELF, sw_br87_mk3_override_windows_year_check, current_year){
	2005..2014 : sw_br87_mk3_override_windows_rev_check ;    //First Barbie
	//2015..4096 :  ;
	sw_mk3_gfx_window_reverse_check ;
}

//Sprite stack

switch(FEAT_TRAINS, SELF, sw_br87_mk3_override_gfx_spritestack, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]){
	1 : sw_br87_mk3_override_gfx ;
	2 : sw_br87_mk3_override_windows_year_check ;
}

switch(FEAT_TRAINS, SELF, sw_br87_mk3_override_gfx_check, livery_choice){
	1 : sw_mk3_sprite_stack ;
	sw_br87_mk3_override_gfx_spritestack ;
}


//Colours

switch(FEAT_TRAINS, SELF, sw_br87_mk3_override_colours, current_year){
	2005..2014 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;    //First Barbie
	//2015..4096 :  ;
	sw_mk3_colours_auto ;
}

switch(FEAT_TRAINS, SELF, sw_br87_mk3_override_colour_check, livery_choice){
	1 : sw_mk3_colours_manual ;
	sw_br87_mk3_override_colours ;
}


//DBSO & DVT checks

//GFX
switch(FEAT_TRAINS, SELF, sw_br87_gfx_dbso_check, DBSO_CHECK){
	1 : sw_dbso_sprite_stack ;
	sw_br87_sprite_stack ;
}

switch(FEAT_TRAINS, SELF, sw_br87_gfx_dvt_check, DVT_CHECK){
	1 : sw_dvt_sprite_stack ;
	sw_br87_gfx_dbso_check ;
}

//Colours

switch(FEAT_TRAINS, SELF, sw_br87_colours_dbso_check, DBSO_CHECK){
	1 : sw_dbso_colours_2cc_check ;
	sw_br87_colours_2cc_check ;
}

switch(FEAT_TRAINS, SELF, sw_br87_colours_dvt_check, DVT_CHECK){
	1 : sw_dvt_colours_2cc_check ;
	sw_br87_colours_dbso_check ;
}

//VFX
switch(FEAT_TRAINS, SELF, sw_br87_vfx_dbso_dvt_check, (DBSO_CHECK) | (DVT_CHECK)){
	0 : vfx_ohle ;
}


//Item


item (FEAT_TRAINS, item_br87, 87){
	property{
		name                           : string(STR_NAME_87) ;
		misc_flags                     : bitmask(TRAIN_FLAG_SPRITE_STACK, TRAIN_FLAG_2CC);
		sprite_id                      : SPRITE_ID_NEW_TRAIN ;
		length                         : 8 ;
		climates_available             : ALL_CLIMATES ;
		introduction_date              : date(1972,1,1) ;
		model_life                     : 28 ;
		vehicle_life                   : 40 ;
		reliability_decay              : 7 ;
		cost_factor                    : 55 ;
		running_cost_base              : RUNNING_COST_ELECTRIC ;
		running_cost_factor            : 250 ;
		refit_cost                     : 0 ;
		refittable_cargo_classes       : 0 ;
		cargo_allow_refit              : [GOOD] ;
		cargo_capacity                 : 1 ;
		track_type                     : ELRL ;
		engine_class                   : ENGINE_CLASS_ELECTRIC ;
		speed                          : 110 mph ;
		power                          : 5000 hp ;
		weight                         : 83 ton ;
		tractive_effort_coefficient    : 0.3108433734939759 ;
		air_drag_coefficient           : 0.0727272727272727 ;
		effect_spawn_model_and_powered : EFFECT_SPAWN_MODEL_ELECTRIC ;
		dual_headed                    : 0 ;
	}
	graphics{
		default                 : sw_br87_gfx_dvt_check ;
		purchase                : sw_br87_purchase ;
		sound_effect            : sw_sfx_electric_loco_old ;
		create_effect           : sw_br87_vfx_dbso_dvt_check ;
		colour_mapping          : sw_br87_colours_dvt_check ;
		purchase_colour_mapping : sw_br87_colours_auto ;
		cargo_subtype_text      : sw_br87_refit_text_checker ;
		purchase_cargo_capacity : 0 ;
		cargo_capacity          : return 0 ;
		can_attach_wagon        : sw_all_but_MU90;
	}
	livery_override(9030){
		default                 : sw_br87_mk3_override_gfx_check ;
		colour_mapping          : sw_br87_mk3_override_colour_check ;
		cargo_subtype_text      : sw_mk3_refit_text_check ;
	}
}