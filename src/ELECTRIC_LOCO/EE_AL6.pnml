//Class 87

//Spritesets

spriteset(gfx_al6_brblue				, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_2(  0,  0)}
spriteset(gfx_al6_brblue_rev			, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_rev_2(  0,  0)}
spriteset(gfx_al6_acblue				, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_2(  0, 25)}
spriteset(gfx_al6_acblue_rev			, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_rev_2(  0, 25)}
spriteset(gfx_al6_intercity				, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_2(  0, 50)}
spriteset(gfx_al6_intercity_rev			, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_rev_2(  0, 50)}
spriteset(gfx_al6_railfreight			, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_2(  0, 75)}
spriteset(gfx_al6_railfreight_rev		, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_rev_2(  0, 75)}
spriteset(gfx_al6_virgin				, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_2(  0,100)}
spriteset(gfx_al6_virgin_rev			, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_rev_2(  0,100)}
spriteset(gfx_al6_freightliner			, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_2(  0,125)}
spriteset(gfx_al6_freightliner_rev		, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_rev_2(  0,125)}
spriteset(gfx_al6_colasrail				, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_2(  0,150)}
spriteset(gfx_al6_colasrail_rev			, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_rev_2(  0,150)}
spriteset(gfx_al6_railexpress			, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_2(  0,175)}
spriteset(gfx_al6_railexpress_rev		, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_rev_2(  0,175)}
spriteset(gfx_al6_ews					, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_2(  0,200)}
spriteset(gfx_al6_ews_rev				, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_rev_2(  0,200)}
spriteset(gfx_al6_anglia				, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_2(  0,225)}
spriteset(gfx_al6_anglia_rev			, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_rev_2(  0,225)}
spriteset(gfx_al6_nse					, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_2(  0,250)}
spriteset(gfx_al6_nse_rev				, "gfx/ELECTRIC_LOCO/class_86.png"){template_8_8_rev_2(  0,250)}

spriteset(gfx_al6_brblue_purchase		, "gfx/ELECTRIC_LOCO/class_86.png"){template_purchase(178,  0)}
spriteset(gfx_al6_acblue_purchase		, "gfx/ELECTRIC_LOCO/class_86.png"){template_purchase(178, 25)}
spriteset(gfx_al6_intercity_purchase	, "gfx/ELECTRIC_LOCO/class_86.png"){template_purchase(178, 50)}
spriteset(gfx_al6_railfreight_purchase	, "gfx/ELECTRIC_LOCO/class_86.png"){template_purchase(178, 75)}
spriteset(gfx_al6_virgin_purchase		, "gfx/ELECTRIC_LOCO/class_86.png"){template_purchase(178,100)}
spriteset(gfx_al6_freightliner_purchase	, "gfx/ELECTRIC_LOCO/class_86.png"){template_purchase(178,125)}
spriteset(gfx_al6_colasrail_purchase	, "gfx/ELECTRIC_LOCO/class_86.png"){template_purchase(178,150)}

spriteset(gfx_al6_headlight				, "gfx/FIXTURES/class_86_headlights.png"){template_8_8_2(0,0)}
spriteset(gfx_al6_taillight				, "gfx/FIXTURES/class_86_headlights.png"){template_8_8_2(0,25)}


//Switches

//Graphics

switch(FEAT_TRAINS, PARENT, sw_al6_gfx_brblue_reverse_check , vehicle_is_reversed){ 		1 : gfx_al6_brblue_rev 		; gfx_al6_brblue ;}
switch(FEAT_TRAINS, PARENT, sw_al6_gfx_acblue_reverse_check , vehicle_is_reversed){ 		1 : gfx_al6_acblue_rev 		; gfx_al6_acblue ;}
switch(FEAT_TRAINS, PARENT, sw_al6_gfx_intercity_reverse_check , vehicle_is_reversed){ 		1 : gfx_al6_intercity_rev 	; gfx_al6_intercity ;}
switch(FEAT_TRAINS, PARENT, sw_al6_gfx_railfreight_reverse_check , vehicle_is_reversed){	1 : gfx_al6_railfreight_rev ; gfx_al6_railfreight ;}
switch(FEAT_TRAINS, PARENT, sw_al6_gfx_virgin_reverse_check , vehicle_is_reversed){			1 : gfx_al6_virgin_rev 		; gfx_al6_virgin ;}
switch(FEAT_TRAINS, PARENT, sw_al6_gfx_freightliner_reverse_check , vehicle_is_reversed){	1 : gfx_al6_freightliner_rev; gfx_al6_freightliner ;}
switch(FEAT_TRAINS, PARENT, sw_al6_gfx_colasrail_reverse_check , vehicle_is_reversed){		1 : gfx_al6_colasrail_rev 	; gfx_al6_colasrail ;}
switch(FEAT_TRAINS, PARENT, sw_al6_gfx_railexpress_reverse_check , vehicle_is_reversed){	1 : gfx_al6_railexpress_rev ; gfx_al6_railexpress ;}
switch(FEAT_TRAINS, PARENT, sw_al6_gfx_ews_reverse_check , vehicle_is_reversed){			1 : gfx_al6_ews_rev 		; gfx_al6_ews ;}
switch(FEAT_TRAINS, PARENT, sw_al6_gfx_anglia_reverse_check , vehicle_is_reversed){			1 : gfx_al6_anglia_rev 		; gfx_al6_anglia ;}
switch(FEAT_TRAINS, PARENT, sw_al6_gfx_nse_reverse_check , vehicle_is_reversed){			1 : gfx_al6_nse_rev 		; gfx_al6_nse ;}


//Automatic liveries

switch(FEAT_TRAINS, SELF, sw_al6_gfx_auto_sectorised, most_common_cargo_type){
	PASS : sw_al6_gfx_intercity_reverse_check ;
	MAIL : sw_al6_gfx_railexpress_reverse_check ;
	sw_al6_gfx_railfreight_reverse_check ;
}

switch(FEAT_TRAINS, SELF, sw_al6_gfx_auto_private1, most_common_cargo_type){
	PASS : sw_al6_gfx_virgin_reverse_check ;
	sw_al6_gfx_freightliner_reverse_check ;
}

switch(FEAT_TRAINS, SELF, sw_al6_gfx_auto_private2, most_common_cargo_type){
	GOOD : sw_al6_gfx_freightliner_reverse_check ;
	FOOD : sw_al6_gfx_freightliner_reverse_check ;
	sw_al6_gfx_colasrail_reverse_check ;
}

switch(FEAT_TRAINS, SELF, sw_al6_gfx_auto, current_year){
	0..1967		: sw_al6_gfx_acblue_reverse_check ;
	1968..1982	: sw_al6_gfx_brblue_reverse_check ;
	1983..1995	: sw_al6_gfx_auto_sectorised ;
	1996..2005  : sw_al6_gfx_auto_private1 ;
	2006..2011  : sw_al6_gfx_freightliner_reverse_check ;
	2012..4096  : sw_al6_gfx_auto_private2 ;
	sw_al6_gfx_brblue_reverse_check ;
}

//Manual liveries

switch(FEAT_TRAINS, SELF, sw_al6_gfx_manual, cargo_subtype){
	0 : sw_al6_gfx_acblue_reverse_check ;
	1 : sw_al6_gfx_brblue_reverse_check ;
	2 : sw_al6_gfx_intercity_reverse_check ;
	3 : sw_al6_gfx_railfreight_reverse_check ;
	4 : sw_al6_gfx_railexpress_reverse_check ;
	5 : sw_al6_gfx_virgin_reverse_check ;
	6 : sw_al6_gfx_freightliner_reverse_check ;
	7 : sw_al6_gfx_colasrail_reverse_check ;
	8 : sw_al6_gfx_ews_reverse_check ;
	9 : sw_al6_gfx_nse_reverse_check ;
	10: sw_al6_gfx_anglia_reverse_check ;
	11: sw_al6_gfx_nse_reverse_check ;
	sw_al6_gfx_brblue_reverse_check ;
}

switch(FEAT_TRAINS, SELF, sw_al6_gfx_manual_check, livery_choice){
	1 : sw_al6_gfx_manual ;
	sw_al6_gfx_auto ;
}

//Lights sprite stacking

switch(FEAT_TRAINS, SELF, sw_al6_taillights, position_in_consist_from_end){
	0 : gfx_al6_taillight ;
}

switch(FEAT_TRAINS, SELF, sw_al6_headlights, position_in_consist){
	0 : gfx_al6_headlight ;
}

switch(FEAT_TRAINS, SELF, sw_al6_sprite_stack, SPR_STACK){
	0 : sw_al6_gfx_manual_check ;
	1 : sw_al6_taillights ;
	2 : sw_al6_headlights ;
}


//Graphics DBSO/DVT checks

switch(FEAT_TRAINS, SELF, sw_al6_gfx_dbso_check, DBSO_CHECK){
	1 : sw_dbso_sprite_stack ;
	sw_al6_sprite_stack ;
}

switch(FEAT_TRAINS, SELF, sw_al6_gfx_dvt_check, DVT_CHECK){
	1 : sw_dvt_sprite_stack ;
	sw_al6_gfx_dbso_check ;
}


//Refit text

switch(FEAT_TRAINS, SELF, sw_al6_refit_text, cargo_subtype){
	0 : string(STR_LIVERY_BRELECTRIC) ;
	1 : string(STR_LIVERY_BRBLUE) ;
	2 : string(STR_LIVERY_INTERCITY) ;
	3 : return(string(STR_LIVERY_RAILFREIGHT,string(STR_SECTOR_GENERAL))) ;
	4 : string(STR_LIVERY_RAIL_EXPRESS) ;
	5 : string(STR_LIVERY_VIRGIN_TRAINS) ;
	6 : string(STR_LIVERY_FREIGHTLINER) ;
	7 : string(STR_LIVERY_COLAS) ;
	8 : string(STR_LIVERY_EWS) ;
	9 : string(STR_LIVERY_NSE) ;
	10: string(STR_LIVERY_ANGLIA_RAIL) ;
	11: string(STR_LIVERY_REG_RAILWAYS) ;
	CB_RESULT_NO_TEXT ;
}

switch(FEAT_TRAINS, SELF, sw_al6_refit_text_check, livery_choice){
	1 : sw_al6_refit_text ;
	CB_RESULT_NO_TEXT ;
}


//Colour mapping

//Automatic


switch(FEAT_TRAINS, SELF, sw_al6_colours_auto_sectorised, most_common_cargo_type){
	PASS : palette_2cc(COLOUR_GREY, COLOUR_RED) ;
	MAIL : palette_2cc(COLOUR_RED, COLOUR_GREY) ;
	palette_2cc(COLOUR_WHITE, COLOUR_GREY) ;
}

switch(FEAT_TRAINS, SELF, sw_al6_colours_auto_private1, most_common_cargo_type){
	PASS : palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
	palette_2cc(COLOUR_DARK_GREEN, COLOUR_YELLOW) ;
}

switch(FEAT_TRAINS, SELF, sw_al6_colours_auto_private2, most_common_cargo_type){
	GOOD : palette_2cc(COLOUR_DARK_GREEN, COLOUR_YELLOW) ;
	palette_2cc(COLOUR_YELLOW, company_colour2) ;
}

switch(FEAT_TRAINS, SELF, sw_al6_colours_auto, current_year){
	0..1967		: palette_2cc(COLOUR_BLUE, COLOUR_WHITE) ;
	1968..1982	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	1983..1995	: sw_al6_colours_auto_sectorised ;
	1996..2005  : sw_al6_colours_auto_private1 ;
	2006..2011  : palette_2cc(COLOUR_DARK_GREEN, COLOUR_YELLOW) ;
	2012..4096  : sw_al6_colours_auto_private2 ;
	palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
}

//Manual

switch(FEAT_TRAINS, SELF, sw_al6_colours_manual, cargo_subtype){
	0 : palette_2cc(COLOUR_BLUE, COLOUR_WHITE) ;
	1 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	2 : palette_2cc(COLOUR_GREY, COLOUR_RED) ;
	3 : palette_2cc(COLOUR_WHITE, COLOUR_GREY) ;
	4 : palette_2cc(COLOUR_RED, COLOUR_GREY) ;
	5 : palette_2cc(COLOUR_RED, COLOUR_WHITE) ;
	6 : palette_2cc(COLOUR_DARK_GREEN, COLOUR_YELLOW) ;
	7 : palette_2cc(COLOUR_YELLOW, company_colour2) ;
	8 : palette_2cc(COLOUR_RED, COLOUR_YELLOW) ;
	9 : palette_2cc(COLOUR_DARK_BLUE, COLOUR_RED) ;
	10: palette_2cc(COLOUR_LIGHT_BLUE, COLOUR_WHITE) ;
	11: palette_2cc(COLOUR_DARK_BLUE, COLOUR_BLUE) ; // Regional Railways
	palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
}

switch(FEAT_TRAINS, SELF, sw_al6_colours_manual_check, livery_choice){
	1 : sw_al6_colours_manual ;
	sw_al6_colours_auto ;
}

switch(FEAT_TRAINS, SELF, sw_al6_colours_2cc_check, colour_scheme_1){
	1 : sw_al6_colours_manual_check ;
	palette_2cc(company_colour1, company_colour2) ;
}



//Colours DBSO/DVT checks

switch(FEAT_TRAINS, SELF, sw_al6_colours_dbso_check, DBSO_CHECK){
	1 : sw_dbso_colours_2cc_check ;
	sw_al6_colours_2cc_check ;
}

switch(FEAT_TRAINS, SELF, sw_al6_colours_dvt_check, DVT_CHECK){
	1 : sw_dvt_colours_2cc_check ;
	sw_al6_colours_dbso_check ;
}


//Purchase spriteset

switch(FEAT_TRAINS, SELF, sw_al6_purchase, current_year){
	0..1967		: gfx_al6_acblue_purchase ;
	1968..1982	: gfx_al6_brblue_purchase ;
	1983..1995	: gfx_al6_intercity_purchase ;
	gfx_al6_railfreight_purchase ;
}

switch(FEAT_TRAINS, SELF, sw_al6_purchase_colours, current_year){
	0..1967		: palette_2cc(COLOUR_BLUE, COLOUR_WHITE) ;
	1968..1982	: palette_2cc(COLOUR_DARK_BLUE, COLOUR_WHITE) ;
	1983..1995	: palette_2cc(COLOUR_GREY, COLOUR_RED) ;
	palette_2cc(COLOUR_WHITE, COLOUR_GREY) ;
}

switch(FEAT_TRAINS, SELF, sw_al6_purchase_colours_2cc_check, colour_scheme_1){
	1 : sw_al6_purchase_colours ;
	palette_2cc(company_colour1, company_colour2) ;
}



//Item


item (FEAT_TRAINS, item_al6, 86){
	property{
		name                           : string(STR_NAME_AL6) ;
		misc_flags                     : bitmask(TRAIN_FLAG_SPRITE_STACK, TRAIN_FLAG_2CC);
		sprite_id                      : SPRITE_ID_NEW_TRAIN ;
		length                         : 8 ;
		climates_available             : ALL_CLIMATES ;
		introduction_date              : date(1965,1,1) ;
		model_life                     : 30 ;
		vehicle_life                   : 53 ;
		reliability_decay              : 9 ;
		cost_factor                    : 48 ;
		running_cost_base              : RUNNING_COST_ELECTRIC ;
		running_cost_factor            : 235 ;
		refit_cost                     : 0 ;
		refittable_cargo_classes       : 0 ;
		cargo_allow_refit              : [MAIL, GOOD] ;
		cargo_capacity                 : 1 ;
		track_type                     : ELRL ;
		engine_class                   : ENGINE_CLASS_ELECTRIC ;
		speed                          : 100 mph ;
		power                          : 3013 kW ;
		weight                         : 86 ton ;
		tractive_effort_coefficient    : 0.3 ;
		air_drag_coefficient           : 0.08 ;
		effect_spawn_model_and_powered : EFFECT_SPAWN_MODEL_ELECTRIC ;
		dual_headed                    : 0 ;
	}
	graphics{
		default                 : sw_al6_gfx_dvt_check ;
		purchase                : sw_al6_purchase ;
		sound_effect            : sw_sfx_electric_loco_old ;
		create_effect           : sw_br87_vfx_dbso_dvt_check ;
		colour_mapping          : sw_al6_colours_dvt_check ;
		purchase_colour_mapping : sw_al6_purchase_colours_2cc_check ;
		cargo_subtype_text      : sw_al6_refit_text_check ;
		purchase_cargo_capacity : 0 ;
		cargo_capacity          : return 0 ;
		can_attach_wagon        : sw_all_but_MU90;
		running_cost_factor     : return(current_speed == 0 ? 40 : 235) ;
		purchase_power          : 3013 ;
		purchase_running_cost_factor : 235 ;
	}
}


if (alt_names == 1){
item (FEAT_TRAINS, item_al6, 86){
	property{
		name                           : string(STR_NAME_AL6_ALT) ;
	}
}
}